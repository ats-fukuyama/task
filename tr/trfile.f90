!     ***********************************************************

!           SAVE TRANSPORT DATA

!     ***********************************************************

      SUBROUTINE TRSAVE

      USE TRCOMM, ONLY : ABB2RHOG, ABRHO, ABRHOG, AD0, AIB2RHOG, AJBST, AJNBT, AJOH, AJOHT, AJRFT, AJT, ALI, ALP, AMZ, ANC,       &
     &                   ANFE, ANNU, ANSAV, AR1RHO, AR1RHOG, AR2RHO, AR2RHOG, ARHBRHOG, ARRHO, ARRHOG, AV0, BB, BETA0, BETAA,     &
     &                   BETAP0, BETAPA, BP, CALF, CDH, CDP, CDW, CHP, CK0, CK1, CKALFA, CKBETA, CKGUMA, CNB, CNH, CNP, CWEB,     &
     &                   DR, DT, DVRHO, DVRHOG, EPSLTR, EPSRHO, ETA, EZOH, IZERO, KFNLOG, KNAMTR, KUFDCG, KUFDEV, LMAXTR, MDCD05, &
     &                   MDDIAG, MDDW, MDLAD, MDLAVK, MDLCD, MDLEC, MDLEOI, MDLEQ0, MDLEQB, MDLEQE, MDLEQN, MDLEQT, MDLEQU,       &
     &                   MDLEQZ, MDLER, MDLETA, MDLFLX, MDLIC, MDLJBS, MDLJQ, MDLKAI, MDLKNC, MDLLH, MDLNB, MDLNF, MDLPCK,        &
     &                   MDLPEL, MDLST, MDLTPF, MDLUF, MDLWLD, MDLXP, MDNCLS, MDNI, MDTC, MODELG, MODEP, NEA, NEQMAX, NGPST,      &
     &                   NGRSTP, NGTSTP, NNS, NRAMAX, NRMAX, NROMAX, NSMAX, NSNMAX, NSS, NST, NSV, NSZMAX, NTEQIT, NTMAX,         &
     &                   NTMAX_SAVE, NTSTEP, PA, PBSCD, PCXT, PECCD, PECNPR, PECR0, PECRW, PECTOE, PECTOT, PELPAT, PELR0, PELRAD, &
     &                   PELRW, PELTIM, PELTOT, PELVEL, PHIA, PICCD, PICNPR, PICR0, PICRW, PICTOE, PICTOT, PIET, PINT, PLHCD,     &
     &                   PLHNPR, PLHR0, PLHRW, PLHTOE, PLHTOT, PN, PNBCD, PNBENG, PNBR0, PNBRTG, PNBRW, PNBT, PNBTOT, PNC, PNFE,  &
     &                   PNNU, PNNUS, PNS, PNSS, POHT, POUT, PRFT, PRLT, PROFJ1, PROFJ2, PROFN1, PROFN2, PROFT1, PROFT2, PROFU1,  &
     &                   PROFU2, PT, PTS, PZ, Q0, RA, RDLT, RDP, RG, RHOA, RHOG, RHOM, RIPE, RIPS, RJCB, RKAP, RKPRHO, RKPRHOG,   &
     &                   RM, RMJRHO, RMNRHO, RN, RNF, RPSI, RR, RT, RTF, RU, RW, T, TAUE1, TAUE2, TAUE89, TIME_INT, TPRE, TPRST,  &
     &                   TS0, TSAV, TSST, TST, TTRHO, TTRHOG, VLOOP, VPOL, VSEC, VTOR, WPPRE, WPT, WST
      IMPLICIT NONE
      INTEGER(4):: IERR, NDD, NDM, NDY, NTH1, NTM1, NTS1
      CHARACTER(LEN=3):: K1, K2, K3, K4, K5, K6


      CALL FWOPEN(21,KNAMTR,0,4,'TR',IERR)
      IF(IERR.NE.0) RETURN

      WRITE(21) MDLEQB,MDLEQN,MDLEQT,MDLEQU,MDLEQZ,MDLEQ0,MDLEQE,MDLEOI
      WRITE(21) NRMAX,NRAMAX,NROMAX,DT,NGPST,TSST
      WRITE(21) NTMAX,NTSTEP,NGTSTP,NGRSTP,NSMAX,NSZMAX,NSNMAX
      WRITE(21) NSS,NSV,NNS,NST,NEA,NEQMAX
      WRITE(21) RR,RA,RKAP,RDLT,BB,RIPS,RIPE,RHOA,PHIA
      WRITE(21) PA,PZ,PN,PNS,PT,PTS
      WRITE(21) PNC,PNFE,PNNU,PNNUS
      WRITE(21) PROFN1,PROFN2,PROFT1,PROFT2,PROFU1,PROFU2,PROFJ1,PROFJ2, &
     &          ALP,AD0,AV0,CNP,CNH,CDP,CDH,CDW,CWEB,CALF,CNB
      WRITE(21) MDLKAI,MDLETA,MDLAD,MDLAVK,MDLJBS,MDLKNC,MDLTPF,MDCD05
      WRITE(21) TPRST,MDLST,MDLNF,IZERO
      WRITE(21) MODELG,NTEQIT
      WRITE(21) MDLXP,MDLUF,MDNCLS,MDLWLD,MDDIAG,MDDW,MDLFLX,MDLER
      WRITE(21) KUFDEV,KUFDCG,TIME_INT,MODEP,MDNI,MDLJQ,MDTC,MDLPCK
      WRITE(21) PNBTOT,PNBR0,PNBRW,PNBENG,PNBRTG,MDLNB
      WRITE(21) PECTOT,PECR0,PECRW,PECTOE,PECNPR,MDLEC
      WRITE(21) PLHTOT,PLHR0,PLHRW,PLHTOE,PLHNPR,MDLLH
      WRITE(21) PICTOT,PICR0,PICRW,PICTOE,PICNPR,MDLIC
      WRITE(21) PNBCD,PECCD,PLHCD,PICCD,PBSCD,MDLCD
      WRITE(21) PELTOT,PELR0,PELRW,PELRAD,PELVEL,PELTIM,PELPAT,MDLPEL
      WRITE(21) DR,PNSS,T,TST,VSEC,WPPRE,TPRE,KFNLOG,NTMAX_SAVE
      WRITE(21) RG,RM,RN,RT,RU,RW,BP,RDP,RPSI,RNF,RTF,ANC,ANFE,ANNU
      WRITE(21) VTOR,VPOL,AJOH,EZOH,ETA,AMZ
      WRITE(21) EPSLTR,LMAXTR,CHP,CK0,CK1,CKALFA,CKBETA,CKGUMA,CNB,CALF
      WRITE(21) DVRHO,TTRHO,ABRHO,ARRHO,AR1RHO,AR2RHO,RMJRHO,RMNRHO,RKPRHO,RJCB,EPSRHO
      WRITE(21) DVRHOG,TTRHOG,ABRHOG,ARRHOG,AR1RHOG,AR2RHOG,ABB2RHOG,AIB2RHOG,ARHBRHOG,RKPRHOG
      WRITE(21) RHOM,RHOG
      CLOSE(21)

      WRITE(6,*) '# DATA WAS SUCCESSFULLY SAVED TO THE FILE.'


!      OPEN(16,POSITION='APPEND',FILE=KFNLOG)
!      OPEN(16,ACCESS='APPEND',FILE=KFNLOG)
      OPEN(16,ACCESS='SEQUENTIAL',FILE=KFNLOG)

         CALL GUDATE(NDY,NDM,NDD,NTH1,NTM1,NTS1)
         WRITE(K1,'(I3)') 100+NDY
         WRITE(K2,'(I3)') 100+NDM
         WRITE(K3,'(I3)') 100+NDD
         WRITE(K4,'(I3)') 100+NTH1
         WRITE(K5,'(I3)') 100+NTM1
         WRITE(K6,'(I3)') 100+NTS1
         WRITE(16,1670) K1(2:3),K2(2:3),K3(2:3),K4(2:3),K5(2:3),K6(2:3), &
     &                  RIPS,RIPE,PN(1),PN(2),BB,PICTOT,PLHTOT,PLHNPR
 1670    FORMAT(' '/ &
     &          ' ','## DATE: ', &
     &              A2,'-',A2,'-',A2,'  ',A2,':',A2,':',A2,' : ', &
     &              '  FILE: ',A40/ &
     &          ' ',3X,'RIPS  =',1PD10.3,'  RIPE  =',1PD10.3, &
     &               '  PNE   =',1PD10.3,'  PNI   =',1PD10.3/ &
     &          ' ',3X,'BB    =',1PD10.3,'  PICTOT=',1PD10.3, &
     &               '  PLHTOT=',1PD10.3,'  PLHNPR=',1PD10.3)
         WRITE(16,1671) T, &
     &                WPT,TAUE1,TAUE2,TAUE89, &
     &                BETAP0,BETAPA,BETA0,BETAA
 1671    FORMAT(' ','# TIME : ',F7.3,' SEC'/ &
     &          ' ',3X,'WPT   =',1PD10.3,'  TAUE  =',1PD10.3, &
     &               '  TAUED =',1PD10.3,'  TAUE89=',1PD10.3/ &
     &          ' ',3X,'BETAP0=',1PD10.3,'  BETAPA=',1PD10.3, &
     &               '  BETA0 =',1PD10.3,'  BETAA =',1PD10.3)

         WRITE(16,1672) WST(1),TS0(1),TSAV(1),ANSAV(1), &
     &                WST(2),TS0(2),TSAV(2),ANSAV(2)
 1672    FORMAT(' ',3X,'WE    =',1PD10.3,'  TE0   =',1PD10.3, &
     &               '  TEAVE =',1PD10.3,'  NEAVE =',1PD10.3/ &
     &          ' ',3X,'WD    =',1PD10.3,'  TD0   =',1PD10.3, &
     &               '  TDAVE =',1PD10.3,'  NDAVE =',1PD10.3)

         WRITE(16,1673) AJT,VLOOP,ALI,Q0, &
     &                AJOHT,AJNBT,AJRFT,AJBST
 1673    FORMAT(' ',3X,'AJT   =',1PD10.3,'  VLOOP =',1PD10.3, &
     &               '  ALI   =',1PD10.3,'  Q0    =',1PD10.3/ &
     &          ' ',3X,'AJOHT =',1PD10.3,'  AJNBT =',1PD10.3, &
     &               '  AJRFT =',1PD10.3,'  AJBST =',1PD10.3)

         WRITE(16,1674) PINT,POHT,PNBT, &
     &                PRFT(1)+PRFT(2)+PRFT(3)+PRFT(4), &
     &                POUT,PRLT,PCXT,PIET
 1674    FORMAT(' ',3X,'PINT  =',1PD10.3,'  POHT  =',1PD10.3, &
     &               '  PNBT  =',1PD10.3,'  PRFT  =',1PD10.3/ &
     &          ' ',3X,'POUT  =',1PD10.3,'  PRLT  =',1PD10.3, &
     &               '  PCXT  =',1PD10.3,'  PIETE =',1PD10.3)

      CLOSE(16)

  900 RETURN
      END SUBROUTINE TRSAVE

!     ***********************************************************

!           LOAD TRANSPORT DATA

!     ***********************************************************

      SUBROUTINE TRLOAD

      USE TRCOMM, ONLY : ABB2RHOG, ABRHO, ABRHOG, AD0, AIB2RHOG, AJOH, ALP, AMZ, ANC, ANFE, ANNU, AR1RHO, AR1RHOG, AR2RHO,      &
     &                   AR2RHOG, ARHBRHOG, ARRHO, ARRHOG, AV0, BB, BP, CALF, CDH, CDP, CDW, CHP, CK0, CK1, CKALFA, CKBETA,     &
     &                   CKGUMA, CNB, CNH, CNP, CWEB, DR, DT, DVRHO, DVRHOG, EPSLTR, EPSRHO, ETA, EZOH, GRG, GRM, IREAD, IZERO, &
     &                   KFNLOG, KNAMTR, KUFDCG, KUFDEV, LMAXTR, MDCD05, MDDIAG, MDDW, MDLAD, MDLAVK, MDLCD, MDLEC, MDLEOI,     &
     &                   MDLEQ0, MDLEQB, MDLEQE, MDLEQN, MDLEQT, MDLEQU, MDLEQZ, MDLER, MDLETA, MDLFLX, MDLIC, MDLJBS, MDLJQ,   &
     &                   MDLKAI, MDLKNC, MDLLH, MDLNB, MDLNF, MDLPCK, MDLPEL, MDLST, MDLTPF, MDLUF, MDLWLD, MDLXP, MDNCLS,      &
     &                   MDNI, MDTC, MODELG, MODEP, NEA, NEQMAX, NGPST, NGR, NGRSTP, NGST, NGT, NGTSTP, NNS, NRAMAX, NRMAX,     &
     &                   NROMAX, NSMAX, NSNMAX, NSS, NST, NSV, NSZMAX, NTEQIT, NTMAX, NTMAX_SAVE, NTSTEP, PA, PBSCD, PECCD,     &
     &                   PECNPR, PECR0, PECRW, PECTOE, PECTOT, PELPAT, PELR0, PELRAD, PELRW, PELTIM, PELTOT, PELVEL, PHIA, PI,  &
     &                   PICCD, PICNPR, PICR0, PICRW, PICTOE, PICTOT, PLHCD, PLHNPR, PLHR0, PLHRW, PLHTOE, PLHTOT, PN, PNBCD,   &
     &                   PNBENG, PNBR0, PNBRTG, PNBRW, PNBTOT, PNC, PNFE, PNNU, PNNUS, PNS, PNSS, PROFJ1, PROFJ2, PROFN1,       &
     &                   PROFN2, PROFT1, PROFT2, PROFU1, PROFU2, PT, PTS, PZ, Q0, QP, RA, RDLT, RDP, RG, RHOA, RHOG, RHOM, RIPE,&
     &                   RIPS, RJCB, RKAP, RKPRHO, RKPRHOG, RM, RMJRHO, RMNRHO, RN, RNF, RPSI, RR, RT, RTF, RU, RW, T, TIME_INT,&
     &                   TPRE, TPRST, TSST, TST, TTRHO, TTRHOG, VPOL, VSEC, VTOR, WPPRE, &
     &                   ALLOCATE_TRCOMM
      IMPLICIT NONE
      INTEGER(4):: IERR, NR
      REAL(8)   :: FCTR


      CALL FROPEN(21,KNAMTR,0,1,'TR',IERR)
      IF(IERR.NE.0) RETURN


      READ(21) MDLEQB,MDLEQN,MDLEQT,MDLEQU,MDLEQZ,MDLEQ0,MDLEQE,MDLEOI
      READ(21) NRMAX,NRAMAX,NROMAX,DT,NGPST,TSST
      READ(21) NTMAX,NTSTEP,NGTSTP,NGRSTP,NSMAX,NSZMAX,NSNMAX
        CALL ALLOCATE_TRCOMM(IERR)
        IF(IERR.NE.0) RETURN
      READ(21) NSS,NSV,NNS,NST,NEA,NEQMAX
      READ(21) RR,RA,RKAP,RDLT,BB,RIPS,RIPE,RHOA,PHIA
      READ(21) PA,PZ,PN,PNS,PT,PTS
      READ(21) PNC,PNFE,PNNU,PNNUS
      READ(21) PROFN1,PROFN2,PROFT1,PROFT2,PROFU1,PROFU2,PROFJ1,PROFJ2, &
     &         ALP,AD0,AV0,CNP,CNH,CDP,CDH,CDW,CWEB,CALF,CNB
      READ(21) MDLKAI,MDLETA,MDLAD,MDLAVK,MDLJBS,MDLKNC,MDLTPF,MDCD05
      READ(21) TPRST,MDLST,MDLNF,IZERO
      READ(21) MODELG,NTEQIT
      READ(21) MDLXP,MDLUF,MDNCLS,MDLWLD,MDDIAG,MDDW,MDLFLX,MDLER
      READ(21) KUFDEV,KUFDCG,TIME_INT,MODEP,MDNI,MDLJQ,MDTC,MDLPCK
      READ(21) PNBTOT,PNBR0,PNBRW,PNBENG,PNBRTG,MDLNB
      READ(21) PECTOT,PECR0,PECRW,PECTOE,PECNPR,MDLEC
      READ(21) PLHTOT,PLHR0,PLHRW,PLHTOE,PLHNPR,MDLLH
      READ(21) PICTOT,PICR0,PICRW,PICTOE,PICNPR,MDLIC
      READ(21) PNBCD,PECCD,PLHCD,PICCD,PBSCD,MDLCD
      READ(21) PELTOT,PELR0,PELRW,PELRAD,PELVEL,PELTIM,PELPAT,MDLPEL
      READ(21) DR,PNSS,T,TST,VSEC,WPPRE,TPRE,KFNLOG,NTMAX_SAVE
      READ(21) RG,RM,RN,RT,RU,RW,BP,RDP,RPSI,RNF,RTF,ANC,ANFE,ANNU
      READ(21) VTOR,VPOL,AJOH,EZOH,ETA,AMZ
      READ(21) EPSLTR,LMAXTR,CHP,CK0,CK1,CKALFA,CKBETA,CKGUMA,CNB,CALF
      READ(21) DVRHO,TTRHO,ABRHO,ARRHO,AR1RHO,AR2RHO,RMJRHO,RMNRHO,RKPRHO,RJCB,EPSRHO
      READ(21) DVRHOG,TTRHOG,ABRHOG,ARRHOG,AR1RHOG,AR2RHOG,ABB2RHOG,AIB2RHOG,ARHBRHOG,RKPRHOG
      READ(21) RHOM,RHOG
      CLOSE(21)

      WRITE(6,*) '# DATA WAS SUCCESSFULLY LOADED FROM THE FILE.'
      IREAD=1

      NGR=0
      NGT=0
      NGST=0
      RIPS=RIPE
      GRG(1)=0.0
      GRM(1:NRMAX)  =SNGL(RM(1:NRMAX))
      GRG(2:NRMAX+1)=SNGL(RG(1:NRMAX))
      QP(1:NRMAX)   =TTRHOG(1:NRMAX)*ARRHOG(1:NRMAX)*DVRHOG(1:NRMAX)/(4.D0*PI**2*RDP(1:NRMAX))
!     *** calculate q_axis ***
      Q0=FCTR(RG(1),RG(2),QP(1),QP(2))

  900 RETURN
      END SUBROUTINE TRLOAD

!     ***********************************************************

!           SAVE GRAPHIC DATA

!     ***********************************************************

      SUBROUTINE TRGRSV

      USE TRCOMM, ONLY : GRG, GRM, GT, GTR, GVR, GVT, NGR, NGT
      IMPLICIT NONE
      INTEGER(4):: IST
      CHARACTER(LEN=32):: TRFLNM
      LOGICAL   :: LEX


    1 WRITE(6,*) '# INPUT : GRAPHIC SAVE FILE NAME (CR TO CANCEL)'
      READ(5,'(A32)',ERR=1,END=900) TRFLNM
      IF(TRFLNM.EQ.'                                ') GOTO 900
      INQUIRE (FILE=TRFLNM,EXIST=LEX)
      IF(LEX) THEN
!         WRITE(6,*) '# OLD FILE IS GOING TO BE OVERWRITTEN.  ',
!     &              'ARE YOU SURE {Y/N}?'
!         READ(5,'(A1)',ERR=1,END=900) KID
!         CALL GUCPTL(KID)
!         IF(KID.NE.'Y') GOTO 1
         OPEN(22,FILE=TRFLNM,IOSTAT=IST,STATUS='OLD',ERR=10,FORM='UNFORMATTED')
         WRITE(6,*) '# OLD FILE (',TRFLNM,') IS ASSIGNED FOR OUTPUT.'
         GOTO 30
   10    WRITE(6,*) '# XX OLD FILE OPEN ERROR : IOSTAT=',IST
         GOTO 1
      ELSE
         OPEN(22,FILE=TRFLNM,IOSTAT=IST,STATUS='NEW',ERR=20,FORM='UNFORMATTED')
         WRITE(6,*) '# NEW FILE (',TRFLNM,') IS CREATED FOR OUTPUT.'
         GOTO 30
   20    WRITE(6,*) 'XX NEW FILE OPEN ERROR : IOSTAT=',IST
         GOTO 1
      ENDIF

   30 WRITE(22) GVR,GRM,GRG,GTR,NGR
      WRITE(22) GVT,GT,NGT
      CLOSE(22)

      WRITE(6,*) '# DATA WAS SUCCESSFULLY SAVED TO THE FILE.'

  900 RETURN
      END SUBROUTINE TRGRSV

!     ***********************************************************

!           LOAD GRAPHIC DATA

!     ***********************************************************

      SUBROUTINE TRGRLD

      USE TRCOMM, ONLY : GRG, GRM, GT, GTR, GVR, GVT, NGR, NGT
      IMPLICIT NONE
      INTEGER(4):: IST
      CHARACTER(LEN=32):: TRFLNM
      LOGICAL   :: LEX


    1 WRITE(6,*) '# INPUT : GRAPHIC LOAD FILE NAME (CR TO CANCEL)'
      READ(5,'(A32)',ERR=1,END=900) TRFLNM
      IF(TRFLNM.EQ.'                                ') GOTO 900
      INQUIRE (FILE=TRFLNM,EXIST=LEX)
      IF(LEX) THEN
         OPEN(22,FILE=TRFLNM,IOSTAT=IST,STATUS='OLD',ERR=10,FORM='UNFORMATTED')
         WRITE(6,*) '# OLD FILE (',TRFLNM,') IS ASSIGNED FOR INPUT.'
         GOTO 30
   10    WRITE(6,*) '# XX OLD FILE OPEN ERROR : IOSTAT=',IST
         GOTO 1
      ELSE
         WRITE(6,*) 'XX FILE (',TRFLNM,') NOT FOUND'
         GOTO 1
      ENDIF
   30 READ(22) GVR,GRM,GRG,GTR,NGR
      READ(22) GVT,GT,NGT
      CLOSE(22)

      WRITE(6,*) '# DATA WAS SUCCESSFULLY LOADED FROM THE FILE.'

  900 RETURN
      END SUBROUTINE TRGRLD

!     ***********************************************************

!           CHECKING WHETHER IMPURITY EXISTS

!     ***********************************************************

      SUBROUTINE CHECK_IMPURITY(MDSLCT)

      USE TRCOMM, ONLY : KUFDCG, KUFDEV, MDNI
      USE TRCOM1, ONLY : KDIRX, NMCHK
      IMPLICIT NONE
      INTEGER(4), INTENT(OUT):: MDSLCT
      INTEGER(4)             :: IKDIRX, IKNDCG, IKNDEV, KL2
      CHARACTER(LEN=80)      :: KDIRR2, KFILE
      CHARACTER(LEN=20)      :: KFID
      LOGICAL LEX


      IKNDEV = len_trim(KUFDEV)
      IKNDCG = len_trim(KUFDCG)

      IKDIRX = len_trim(KDIRX)
      KDIRR2=KDIRX(1:IKDIRX)//KUFDEV(1:IKNDEV)//'2d'//KUFDCG(1:IKNDCG)//'.'
      KL2 = len_trim(KDIRR2)

      IF(MDNI.LT.0.OR.MDNI.GT.3) MDNI=0
      MDSLCT=0

      KFID='ZEFFR'
      KFILE=KDIRR2(1:KL2)//KFID
      INQUIRE(FILE=KFILE,EXIST=LEX,ERR=9000)
      IF(LEX) MDSLCT=MDSLCT+1
      KFID='NM1'
      KFILE=KDIRR2(1:KL2)//KFID
      INQUIRE(FILE=KFILE,EXIST=LEX,ERR=9000)
      IF(LEX) THEN
         NMCHK=0
         MDSLCT=MDSLCT+2
         KFID='NM2'
         KFILE=KDIRR2(1:KL2)//KFID
         INQUIRE(FILE=KFILE,EXIST=LEX,ERR=9000)
         IF(LEX) NMCHK=1
         KFID='NM3'
         KFILE=KDIRR2(1:KL2)//KFID
         INQUIRE(FILE=KFILE,EXIST=LEX,ERR=9000)
         IF(LEX) NMCHK=2
      ENDIF
      KFID='NIMP'
      KFILE=KDIRR2(1:KL2)//KFID
      INQUIRE(FILE=KFILE,EXIST=LEX,ERR=9000)
      IF(LEX) MDSLCT=MDSLCT+4

 9000 RETURN
      END SUBROUTINE CHECK_IMPURITY

!     ***********************************************************

!           CONTROL ROUTINE FOR UFILE READING

!     ***********************************************************

      SUBROUTINE TR_UFILE_CONTROL(NSW)

!     MDNI is a parameter that can control which data is used
!     to determine bulk density, impurity density or effective
!     charge number among those data.
!     If all the data above do not exist, MDNI is set to zero
!     automatically regardless of original MDNI.
!           0 : NSMAX=2, ne=ni
!           1 : calculate nimp and ni profiles from NE, ZIMP and ZEFFR
!           2 : calculate nimp and zeff profiles from NE, ZIMP and NM1
!           3 : calculate zeff and ni profiles from NE, ZIMP and NIMP

!     For the time being, we deal with transport calculation with
!     three particles so that densities of 4th argument requires only
!     finite small values and their values can be ignored in the context
!     of charge neutrality.

      USE TRCOMM, ONLY : AD0, CDP, CNP, DR, KUFDCG, KUFDEV, MDLFLX, MDNI, NEQMAX, NRAMAX, NRMAX, NROMAX, NSMAX, NST, RHOA
      IMPLICIT NONE
      INTEGER(4), INTENT(IN):: NSW
      INTEGER(4):: MDSLCT,  NEQ, NEQL


      NRAMAX=INT(RHOA*NRMAX)
      DR = 1.D0/DBLE(NRMAX)

      IF(NSW.NE.0) THEN
         CALL CHECK_IMPURITY(MDSLCT)

         select case(MDSLCT)
         case(0)
            MDNI=0
            NEQL=0
            DO NEQ=1,NEQMAX
               IF(NST(NEQ).NE.0) NEQL=NEQL+1
            ENDDO
            IF(NEQL.EQ.1) THEN
               NSMAX=1
            ELSE
               NSMAX=2
            ENDIF
         case(1)
            IF(MDNI.EQ.2.OR.MDNI.EQ.3) MDNI=1
         case(2)
            IF(MDNI.EQ.1.OR.MDNI.EQ.3) MDNI=2
         case(3)
            IF(MDNI.EQ.3) MDNI=1
         case(4)
            IF(MDNI.EQ.1.OR.MDNI.EQ.2) MDNI=3
         case(5)
            IF(MDNI.EQ.2) MDNI=1
         case(6)
            IF(MDNI.EQ.1) MDNI=2
         end select

         IF(MDLFLX.NE.0) THEN
            CNP=0.D0
            CDP=0.D0
            AD0=0.D0
         ENDIF

         WRITE(6,'(A7,A8,A6,A15)') 'DEVICE=',KUFDEV,'SHOT#=',KUFDCG

         select case(NSW)
         case(1)
            CALL TR_TIME_UFILE
         case(2)
            CALL TR_STEADY_UFILE
         case(3)
            CALL TR_TIME_UFILE_TOPICS
         end select
      ENDIF

      NROMAX = NRMAX
      NRMAX  = NRAMAX

 9000 RETURN
      END SUBROUTINE TR_UFILE_CONTROL

!     ***********************************************************

!           STEADY STATE UFILE DATA INPUT ROUTINE

!     ***********************************************************

      SUBROUTINE TR_STEADY_UFILE

      USE TRCOMM, ONLY : ABRHOU, AJNBU, AJU, AR1RHOU, AR2RHOU, ARRHOU, BB, BBU, BPU, DR, DVRHOU, KUFDCG, KUFDEV, MDLJQ, MDLXP, &
     &                   MDNI, MDPHIA, NRAMAX, NRM, NRMAX, NRMP, NRUM, NTS, NTUM, PBMU, PECU, PHIA, PHIAU, PICU, PN, PNBU, PNS,&
     &                   PNSA, POHU, PRLU, PT, PTS, PTSA, PZ, QPU, RA, RAU, RHOA, RIPE, RIPS, RIPU, RKAP, RKAPU, RKPRHOU,      &
     &                   RMJRHOU, RMNRHOU, RNFU, RNU, RNU_ORG, RR, RRU, RT, RTU, SNBU, SWLU, TIME_INT, TTRHOU, VOLAU, WROTU,   &
     &                   ZEFFU, ZEFFU_ORG
      USE TRCOM1, ONLY : GRE, KDIRX, NMCHK, NREMAX, NTXMAX, NTXMAX1, RNEXEU, RNEXU, RTEXEU, RTEXU, RTIXEU, RTIXU, TMU, TMU1
      IMPLICIT NONE
      INTEGER(4):: IERR, MDAMIN, MDBT, MDCHK, MDIP, MDKAPPA, MDRGEO, MDSUM, NR, NRFMAX, NRLMAX, NTX, NTX1
      REAL(8)   :: AMP, DT_MIN, PNF1, PNF2, PNIMP1, PNIMP2, PNM1, PNM2, PNM3, PNM4, PNM5, PNM6, PTMP1, PTMP2, PZEF1, PZEF2
      REAL(8),DIMENSION(NRUM) :: RUF, RL
      REAL(8),DIMENSION(NTUM) :: F1, TL
      REAL(8),DIMENSION(NRMP) :: FAS
      REAL(8),DIMENSION(NTUM,NRUM)::  F2
      CHARACTER(LEN=10)       :: KFID


      MDRGEO=0
      MDAMIN=0
      MDIP=0
      MDBT=0
      MDKAPPA=0
      MDPHIA=0

      NTS=0

!     *** 1D VALUE ***

      KFID='RGEO'
      IF(MDLXP.EQ.0) THEN
         CALL UFREAD_TIME(KDIRX,KUFDEV,KUFDCG,KFID,TMU1,F1,NTXMAX1,NTUM,MDCHK,IERR)
      ELSE
         CALL IPDB_MDS1(KUFDEV,KUFDCG,KFID,NTUM,TMU1,F1,NTXMAX1,IERR)
      ENDIF
      IF(IERR.EQ.1) THEN
         MDRGEO=IERR
         IERR=0
      ELSE
         RRU(1:NTXMAX1)=F1(1:NTXMAX1)
      ENDIF

      KFID='AMIN'
      IF(MDLXP.EQ.0) THEN
         CALL UFREAD_TIME(KDIRX,KUFDEV,KUFDCG,KFID,TMU1,F1,NTXMAX1,NTUM,MDCHK,IERR)
      ELSE
         CALL IPDB_MDS1(KUFDEV,KUFDCG,KFID,NTUM,TMU1,F1,NTXMAX1,IERR)
      ENDIF
      IF(IERR.EQ.1) THEN
         MDAMIN=IERR
         IERR=0
      ELSE
         RAU(1:NTXMAX1)=F1(1:NTXMAX1)
      ENDIF

      KFID='IP'
      IF(MDLXP.EQ.0) THEN
         CALL UFREAD_TIME(KDIRX,KUFDEV,KUFDCG,KFID,TMU1,F1,NTXMAX1,NTUM,MDCHK,IERR)
      ELSE
         CALL IPDB_MDS1(KUFDEV,KUFDCG,KFID,NTUM,TMU1,F1,NTXMAX1,IERR)
      ENDIF
      IF(IERR.EQ.1) THEN
         MDIP=IERR
         IERR=0
      ELSE
         RIPU(1:NTXMAX1)=ABS(F1(1:NTXMAX1)*1.D-6)
      ENDIF

      KFID='BT'
      IF(MDLXP.EQ.0) THEN
         CALL UFREAD_TIME(KDIRX,KUFDEV,KUFDCG,KFID,TMU1,F1,NTXMAX1,NTUM,MDCHK,IERR)
      ELSE
         CALL IPDB_MDS1(KUFDEV,KUFDCG,KFID,NTUM,TMU1,F1,NTXMAX1,IERR)
      ENDIF
      IF(IERR.EQ.1) THEN
         MDBT=IERR
         IERR=0
      ELSE
         BBU(1:NTXMAX1)=ABS(F1(1:NTXMAX1))
      ENDIF

      IF(KUFDEV.EQ.'tftr'.AND.(KUFDCG.EQ.'50862' .OR. KUFDCG.EQ.'50921'.OR.KUFDCG.EQ.'52527')) THEN
         RKAPU(1:NTXMAX1)=1.D0
      ELSE
         KFID='KAPPA'
         IF(MDLXP.EQ.0) THEN
            CALL UFREAD_TIME(KDIRX,KUFDEV,KUFDCG,KFID,TMU1,F1,NTXMAX1,NTUM,MDCHK,IERR)
         ELSE
            CALL IPDB_MDS1(KUFDEV,KUFDCG,KFID,NTUM,TMU1,F1,NTXMAX1,IERR)
         ENDIF
         IF(IERR.EQ.1) THEN
            MDKAPPA=IERR
            IERR=0
         ELSE
            RKAPU(1:NTXMAX1)=F1(1:NTXMAX1)
         ENDIF
      ENDIF
!      IF(IERR.NE.0) STOP 'SOME 1D UFILES DO NOT EXIST.'

      KFID='PHIA'
      IF(MDLXP.EQ.0) THEN
         CALL UFREAD_TIME(KDIRX,KUFDEV,KUFDCG,KFID,TMU1,F1,NTXMAX1,NTUM,MDCHK,IERR)
      ELSE
         CALL IPDB_MDS1(KUFDEV,KUFDCG,KFID,NTUM,TMU1,F1,NTXMAX1,IERR)
      ENDIF
      IF(IERR.EQ.1) THEN
         MDPHIA=IERR
         IERR=0
      ELSE
         PHIAU(1:NTXMAX1)=F1(1:NTXMAX1)
      ENDIF

!     *** 2D VALUE ***

      AMP=1.D-3
      KFID='TE'
      CALL UF2DSP(KFID,KUFDEV,KUFDCG,DR,PTMP1,PTMP2,FAS,AMP,RHOA,NRAMAX,NRMAX,MDLXP,IERR)
      RTU(1,1:NRMAX,1)=FAS(1:NRMAX)
      RT(1:NRMAX,1)=FAS(1:NRMAX)
      PT(1)=RT(1,1)
      PTS(1)=PTMP1
      IF(RHOA.NE.1.D0) PTSA(1)=PTMP2

      KFID='TEXP'
      IF(MDLXP.EQ.0) THEN
         CALL UFREAD2_ERROR(KDIRX,KUFDEV,KUFDCG,KFID,RUF,TMU,RTEXU,NRFMAX,NTXMAX,NRUM,NTUM,MDCHK,IERR)
      ELSE
         CALL IPDB_RAW2(KUFDEV,KUFDCG,KFID,NRUM,NTUM,RUF,TMU,RTEXU,NRFMAX,NTXMAX,IERR)
      ENDIF
      IF(IERR.EQ.0) THEN
         NREMAX(1)=NRFMAX
         GRE(1:NRFMAX,1)=SNGL(RUF(1:NRFMAX))
      ENDIF
      KFID='TEXPEB'
      IF(MDLXP.EQ.0) THEN
         CALL UFREAD2_ERROR(KDIRX,KUFDEV,KUFDCG,KFID,RUF,TMU,RTEXEU,NRFMAX,NTXMAX,NRUM,NTUM,MDCHK,IERR)
      ELSE
         CALL IPDB_RAW2(KUFDEV,KUFDCG,KFID,NRUM,NTUM,RUF,TMU,RTEXEU,NRFMAX,NTXMAX,IERR)
      ENDIF
!
      KFID='TI'
      CALL UF2DSP(KFID,KUFDEV,KUFDCG,DR,PTMP1,PTMP2,FAS,AMP,RHOA,NRAMAX,NRMAX,MDLXP,IERR)
      RTU(1,1:NRMAX,2)=FAS(1:NRMAX)
      RTU(1,1:NRMAX,3)=FAS(1:NRMAX)
      RT(1:NRMAX,2)=FAS(1:NRMAX)
      RT(1:NRMAX,3)=FAS(1:NRMAX)
      PT  (2)=RT(1,2)
      PT  (3)=RT(1,3)
      PT  (4)=RT(1,2)
      PTS (2)=PTMP1
      PTS (3)=PTMP1
      PTS (4)=PTMP1
      IF(RHOA.NE.1.D0) THEN
         PTSA(2)=PTMP2
         PTSA(3)=PTMP2
         PTSA(4)=PTMP2
      ENDIF

      KFID='TIXP'
      IF(MDLXP.EQ.0) THEN
         CALL UFREAD2_ERROR(KDIRX,KUFDEV,KUFDCG,KFID,RUF,TMU,RTIXU,NRFMAX,NTXMAX,NRUM,NTUM,MDCHK,IERR)
      ELSE
         CALL IPDB_RAW2(KUFDEV,KUFDCG,KFID,NRUM,NTUM,RUF,TMU,RTIXU,NRFMAX,NTXMAX,IERR)
      ENDIF
      KFID='TIXPEB'
      IF(MDLXP.EQ.0) THEN
         CALL UFREAD2_ERROR(KDIRX,KUFDEV,KUFDCG,KFID,RUF,TMU,RTIXEU,NRFMAX,NTXMAX,NRUM,NTUM,MDCHK,IERR)
      ELSE
         CALL IPDB_RAW2(KUFDEV,KUFDCG,KFID,NRUM,NTUM,RUF,TMU,RTIXEU,NRFMAX,NTXMAX,IERR)
      ENDIF

      AMP=1.D-20
      KFID='NE'
      CALL UF2DSP(KFID,KUFDEV,KUFDCG,DR,PTMP1,PTMP2,FAS,AMP,RHOA,NRAMAX,NRMAX,MDLXP,IERR)
      RNU(1,1:NRMAX,1)     = FAS(1:NRMAX)
      RNU(1,1:NRMAX,2)     = FAS(1:NRMAX)
      RNU_ORG(1,1:NRMAX,1) = FAS(1:NRMAX)
      RNU_ORG(1,1:NRMAX,2) = FAS(1:NRMAX)
      ZEFFU(1,1:NRMAX)     = 1.D0 ! in case of MDNI=0
      ZEFFU_ORG(1,1:NRMAX) = 1.D0 ! in case of MDNI=0
      PN(1)  =RNU(1,1,1)
      PN(2)  =RNU(1,1,2)
      PN(3)  =1.D-7
      PN(4)  =1.D-7
      PNS(1) =PTMP1
      PNS(2) =PNS(1)
      PNS(3) =1.D-8
      PNS(4) =1.D-8
      IF(RHOA.NE.1.D0) THEN
         PNSA(1)=PTMP2
         PNSA(2)=PNSA(1)
         PNSA(3)=1.D-8
         PNSA(4)=1.D-8
      ENDIF

      KFID='NEXP'
      IF(MDLXP.EQ.0) THEN
         CALL UFREAD2_ERROR(KDIRX,KUFDEV,KUFDCG,KFID,RUF,TMU,RNEXU,NRFMAX,NTXMAX,NRUM,NTUM,MDCHK,IERR)
      ELSE
         CALL IPDB_RAW2(KUFDEV,KUFDCG,KFID,NRUM,NTUM,RUF,TMU,RNEXU,NRFMAX,NTXMAX,IERR)
      ENDIF
      IF(IERR.EQ.0) THEN
         NREMAX(2)=NRFMAX
         GRE(1:NRFMAX,2)=SNGL(RUF(1:NRFMAX))
      ENDIF
      KFID='NEXPEB'
      IF(MDLXP.EQ.0) THEN
         CALL UFREAD2_ERROR(KDIRX,KUFDEV,KUFDCG,KFID,RUF,TMU,RNEXEU,NRFMAX,NTXMAX,NRUM,NTUM,MDCHK,IERR)
      ELSE
         CALL IPDB_RAW2(KUFDEV,KUFDCG,KFID,NRUM,NTUM,RUF,TMU,RNEXEU,NRFMAX,NTXMAX,IERR)
      ENDIF

      KFID='NFAST'
      CALL UF2DSP(KFID,KUFDEV,KUFDCG,DR,PNF1,PNF2,FAS,AMP,RHOA,NRMAX,NRMAX,MDLXP,IERR)
      DO NR=1,NRMAX
         IF(IERR.EQ.0.AND.FAS(NR).LE.0.D0) FAS(NR)=1.D-10
         RNFU(1,NR)=FAS(NR)
      ENDDO

 100  CONTINUE
      KFID='NM1'
      CALL UF2DSP(KFID,KUFDEV,KUFDCG,DR,PNM1,PNM2,FAS,AMP,RHOA,NRAMAX,NRMAX,MDLXP,IERR)
      DO NR=1,NRMAX
         IF(NMCHK.EQ.0) THEN
            IF(MDNI.NE.0) RNU(1,NR,2)=FAS(NR)
            IF(IERR.EQ.0) RNU_ORG(1,NR,2)=FAS(NR)
            RNU_ORG(1,NR,4)=0.D0
         ELSE
            RNU_ORG(1,NR,4)=FAS(NR)
         ENDIF
      ENDDO
      IF(NMCHK.GE.1) THEN
         KFID='NM2'
         CALL UF2DSP(KFID,KUFDEV,KUFDCG,DR,PNM3,PNM4,FAS,AMP,RHOA,NRAMAX,NRMAX,MDLXP,IERR)
         IF(MDNI.NE.0) RNU(1,1:NRMAX,2)=FAS(1:NRMAX)+RNU_ORG(1,1:NRMAX,4)
         RNU_ORG(1,1:NRMAX,2)=FAS(1:NRMAX)
         PNM1=PNM1+PNM3
         PNM2=PNM2+PNM4
      ENDIF
      IF(NMCHK.GE.2) THEN
         KFID='NM3'
         CALL UF2DSP(KFID,KUFDEV,KUFDCG,DR,PNM5,PNM6,FAS,AMP,RHOA,NRAMAX,NRMAX,MDLXP,IERR)
         IF(MDNI.NE.0) RNU(1,1:NRMAX,2)=RNU(1,1:NRMAX,2)+FAS(1:NRMAX)
         RNU_ORG(1,1:NRMAX,4)=RNU_ORG(1,1:NRMAX,4)+FAS(1:NRMAX)
         PNM1=PNM1+PNM5
         PNM2=PNM2+PNM6
      ENDIF

      AMP=1.D0
      KFID='ZEFFR'
      CALL UF2DSP(KFID,KUFDEV,KUFDCG,DR,PZEF1,PZEF2,FAS,AMP,RHOA,NRAMAX,NRMAX,MDLXP,IERR)
      IF(MDNI.NE.0) ZEFFU(1,1:NRMAX)=FAS(1:NRMAX)
      ZEFFU_ORG(1,1:NRMAX)=FAS(1:NRMAX)

      AMP=1.D-20
      KFID='NIMP'
      CALL UF2DSP(KFID,KUFDEV,KUFDCG,DR,PNIMP1,PNIMP2,FAS,AMP,RHOA,NRAMAX,NRMAX,MDLXP,IERR)
      IF(MDNI.NE.0) RNU(1,1:NRMAX,3)=FAS(1:NRMAX)
      RNU_ORG(1,1:NRMAX,3)=FAS(1:NRMAX)

      select case(MDNI)
      case(1)
         
      DO NR=1,NRMAX
         RNU(1,NR,2)=( (PZ(3)-ZEFFU(1,NR))*RNU(1,NR,1)-(PZ(3)-PZ(2))*PZ(2)*RNFU(1,NR))/(PZ(2)*(PZ(3)-PZ(2)))
         RNU(1,NR,3)=(PZ(2)-ZEFFU(1,NR))/(PZ(3)*(PZ(2)-PZ(3)))*RNU(1,NR,1)
      ENDDO
      PN(2)=RNU(1,1,2)
      PN(3)=RNU(1,1,3)
      PN(4)=1.D-7
      PNS(2)=( (PZ(3)-PZEF1)*PNS(1)-(PZ(3)-PZ(2))*PZ(2)*PNF1)/(PZ(2)*(PZ(3)-PZ(2)))
      PNS(3)=(PZ(2)-PZEF1)/(PZ(3)*(PZ(2)-PZ(3)))*PNS(1)
      PNS(4)=1.D-8
      IF(RHOA.NE.1.D0) THEN
         PNSA(2)=( (PZ(3)-PZEF2)*PNSA(1)-(PZ(3)-PZ(2))*PZ(2)*PNF2)/(PZ(2)*(PZ(3)-PZ(2)))
         PNSA(3)=(PZ(2)-PZEF2)/(PZ(3)*(PZ(2)-PZ(3)))*PNSA(1)
         PNSA(4)=1.D-8
      ENDIF

      case(2)

      DO NR=1,NRMAX
         RNU(1,NR,3)=(RNU(1,NR,1)-( RNU(1,NR,2)+RNFU(1,NR))*PZ(2))/ PZ(3)
         ZEFFU(1,NR)=PZ(2)*(PZ(2)-PZ(3))*(RNU(1,NR,2)+RNFU(1,NR)) / RNU(1,NR,1)+PZ(3)
      ENDDO
      PN(2) =RNU(1,1,2)
      PN(3) =RNU(1,1,3)
      PN(4) =1.D-7
      IF(PNM1.LE.1.D-3) THEN
         PNS(1)=1.D-2
         PNM1=1.D-2-1.D-3
      ENDIF
      PNS(2)=PNM1
      PNS(3)=(PNS(1)-(PNM1+PNF1)*PZ(2))/PZ(3)
      PNS(4)=1.D-8
      IF(RHOA.NE.1.D0) THEN
         PNSA(2)=PNM2
         PNSA(3)=(PNSA(1)-(PNM2+PNF2)*PZ(2))/PZ(3)
         PNSA(4)=1.D-8
      ENDIF

      case(3)

      DO NR=1,NRMAX
         RNU(1,NR,2)=(RNU(1,NR,1)-PZ(2)*RNFU(1,NR)-PZ(3)*RNU(1,NR,3)) /PZ(2)
         ZEFFU(1,NR)=PZ(2)+PZ(3)*(PZ(3)-PZ(2))*(RNU(1,NR,3)/RNU(1,NR,1))
      ENDDO
      PNS(2)=(PNS(1)-PZ(2)*PNF1-PZ(3)*PNIMP1)/PZ(2)
      PNS(3)=PNIMP1
      PNS(4)=1.D-8
      IF(RHOA.NE.1.D0) THEN
         PNSA(2)=(PNSA(1)-PZ(2)*PNF2-PZ(3)*PNIMP2)/PZ(2)
         PNSA(3)=PNIMP2
         PNSA(4)=1.D-8
      ENDIF

      end select
      IF(PNS(3).LE.1.D-8) PNS(3)=1.D-8

      IF(MDNI.NE.0) THEN
!     check whether density developed is appropriate (positive) or not
      DO NR=1,NRMAX
         IF(RNU(1,NR,2).LE.0.D0.OR.RNU(1,NR,3).LE.0.D0) THEN
            WRITE(6,*)'XX TR_STEADY_UFILE: DENSITY NEGATIVE: WRONG MDNI=',MDNI
            MDNI=MDNI+1
            IF(MDNI.LE.3) THEN
               GOTO 100
            ELSE
               STOP
            ENDIF
         ENDIF
      ENDDO
      ENDIF

      AMP=1.D0
      KFID='PBEAM'
      CALL UF2DS(KFID,KUFDEV,KUFDCG,DR,TMU,FAS,AMP,NRMAX,0,1,MDLXP,IERR)
      PBMU(1,1:NRMAX)=FAS(1:NRMAX)

      KFID='Q'
      CALL UF2DS(KFID,KUFDEV,KUFDCG,DR,TMU,FAS,AMP,NRMAX,1,1,MDLXP,IERR)
      IF(IERR.NE.0.AND.MDLJQ.EQ.1) MDLJQ=0
      QPU(1,1:NRMAX)=FAS(1:NRMAX)

      KFID='CURTOT'
      CALL UF2DS(KFID,KUFDEV,KUFDCG,DR,TMU,FAS,AMP,NRMAX,0,1,MDLXP,IERR)
      IF(IERR.NE.0.AND.MDLJQ.EQ.0) MDLJQ=1
      AJU(1,1:NRMAX)=FAS(1:NRMAX)

      KFID='CURNBI'
      CALL UF2DS(KFID,KUFDEV,KUFDCG,DR,TMU,FAS,AMP,NRMAX,0,1,MDLXP,IERR)
      AJNBU(1,1:NRMAX)=FAS(1:NRMAX)

      KFID='BPOL'
      CALL UF2DS(KFID,KUFDEV,KUFDCG,DR,TMU,FAS,AMP,NRMAX,1,0,MDLXP,IERR)
      BPU(1,1:NRMAX)=FAS(1:NRMAX)

      KFID='QNBIE'
      CALL UF2DS(KFID,KUFDEV,KUFDCG,DR,TMU,FAS,AMP,NRMAX,0,1,MDLXP,IERR)
      forall(NR=1:NRMAX,FAS(NR) < 0.d0) FAS(NR) = 0.D0
      PNBU(1,1:NRMAX,1) = FAS(1:NRMAX)

      KFID='QNBII'
      CALL UF2DS(KFID,KUFDEV,KUFDCG,DR,TMU,FAS,AMP,NRMAX,0,1,MDLXP,IERR)
      forall(NR=1:NRMAX,FAS(NR) < 0.d0) FAS(NR) = 0.D0
      PNBU(1,1:NRMAX,2) = FAS(1:NRMAX)

      PICU(1,1:NRMAX,1)=0.D0
      PICU(1,1:NRMAX,2)=0.D0
      PECU(1,1:NRMAX  )=0.D0
      POHU(1,1:NRMAX  )=0.D0
      WROTU(1,1:NRMAX )=0.D0
      KFID='QICRHE'
      CALL UF2DS(KFID,KUFDEV,KUFDCG,DR,TMU,FAS,AMP,NRMAX,0,1,MDLXP,IERR)
      forall(NR=1:NRMAX,FAS(NR) < 0.d0) FAS(NR) = 0.D0
      PICU(1,1:NRMAX,1) = FAS(1:NRMAX)

      KFID='QICRHI'
      CALL UF2DS(KFID,KUFDEV,KUFDCG,DR,TMU,FAS,AMP,NRMAX,0,1,MDLXP,IERR)
      forall(NR=1:NRMAX,FAS(NR) < 0.d0) FAS(NR) = 0.D0
      PICU(1,1:NRMAX,2) = FAS(1:NRMAX)

      KFID='QECH'
      CALL UF2DS(KFID,KUFDEV,KUFDCG,DR,TMU,FAS,AMP,NRMAX,0,1,MDLXP,IERR)
      forall(NR=1:NRMAX,FAS(NR) < 0.d0) FAS(NR) = 0.D0
      PECU(1,1:NRMAX) = FAS(1:NRMAX)

      KFID='QECHE'
      CALL UF2DS(KFID,KUFDEV,KUFDCG,DR,TMU,FAS,AMP,NRMAX,0,1,MDLXP,IERR)
      forall(NR=1:NRMAX,FAS(NR) < 0.d0) FAS(NR) = 0.D0
      PECU(1,1:NRMAX) = FAS(1:NRMAX)

      KFID='QECHI'
      CALL UF2DS(KFID,KUFDEV,KUFDCG,DR,TMU,FAS,AMP,NRMAX,0,1,MDLXP,IERR)
      forall(NR=1:NRMAX,FAS(NR) < 0.d0) FAS(NR) = 0.D0
      PICU(1,1:NRMAX,2) = PICU(1,1:NRMAX,2) + FAS(1:NRMAX)

      KFID='QRAD'
      CALL UF2DS(KFID,KUFDEV,KUFDCG,DR,TMU,FAS,AMP,NRMAX,0,1,MDLXP,IERR)
      PRLU(1,1:NRMAX)=FAS(1:NRMAX)

      KFID='QOHM'
      CALL UF2DS(KFID,KUFDEV,KUFDCG,DR,TMU,FAS,AMP,NRMAX,0,1,MDLXP,IERR)
      POHU(1,1:NRMAX)=FAS(1:NRMAX)

      AMP=1.D-20
      KFID='SNBIE'
      CALL UF2DS(KFID,KUFDEV,KUFDCG,DR,TMU,FAS,AMP,NRMAX,0,1,MDLXP,IERR)
      SNBU(1,1:NRMAX,1)=FAS(1:NRMAX)

      KFID='SNBII'
      CALL UF2DS(KFID,KUFDEV,KUFDCG,DR,TMU,FAS,AMP,NRMAX,0,1,MDLXP,IERR)
      SNBU(1,1:NRMAX,2)=FAS(1:NRMAX)

      KFID='SWALL'
      CALL UF2DS(KFID,KUFDEV,KUFDCG,DR,TMU,FAS,AMP,NRMAX,0,1,MDLXP,IERR)
      SWLU(1,1:NRMAX)=FAS(1:NRMAX)

      AMP=1.D0
      KFID='VROT'
      CALL UF2DS(KFID,KUFDEV,KUFDCG,DR,TMU,FAS,AMP,NRMAX,1,1,MDLXP,IERR)
      WROTU(1,1:NRMAX)=FAS(1:NRMAX)

!     *****

      MDSUM=MDRGEO+MDAMIN+MDIP+MDBT+MDKAPPA+MDPHIA
      IF(MDSUM.NE.0) THEN
         IF(MDRGEO .NE.0) RRU  (1:NTXMAX)=RR
         IF(MDAMIN .NE.0) RAU  (1:NTXMAX)=RA
         IF(MDIP   .NE.0) RIPU (1:NTXMAX)=RIPS
         IF(MDBT   .NE.0) BBU  (1:NTXMAX)=BB
         IF(MDKAPPA.NE.0) RKAPU(1:NTXMAX)=RKAP
         IF(MDPHIA .NE.0) PHIAU(1:NTXMAX)=0.D0
      ENDIF

!     *** in case of discharge with only one time slice ****
!     *     obtain time point designated by 2d ufiles      *
!     *     from time data array attained by 1d ufiles     *
!     *** in case of discharge with mulitple time slices ***
!     *     obtain time point designated by user           *
!     *     from time data array attained by 2d ufiles     *
!     ******************************************************

      IF(TMU(2).EQ.0.D0) THEN
         DT_MIN=ABS(TMU1(1)-TMU(1))
         DO NTX1=2,NTXMAX1
            IF(DT_MIN.GT.ABS(TMU1(NTX1)-TMU(1))) THEN
               DT_MIN=ABS(TMU1(NTX1)-TMU(1))
               NTS=NTX1
            ENDIF
         ENDDO
         IF(NTS.EQ.0) NTS=1
      ELSE
         DT_MIN=ABS(TMU(1)-TIME_INT)
         DO NTX=2,NTUM
            IF(DT_MIN.GT.ABS(TMU(NTX)-TIME_INT)) THEN
               DT_MIN=ABS(TMU(NTX)-TIME_INT)
               NTS=NTX
            ENDIF
         ENDDO
         IF(NTS.EQ.0) NTS=1
      ENDIF

!     *** GEOMETRY FACTORS ***

      KFID='RMAJOR'
      CALL UF2DS(KFID,KUFDEV,KUFDCG,DR,TMU,FAS,AMP,NRMAX,1,1,MDLXP,IERR)
      RMJRHOU(1,1:NRMAX)=FAS(1:NRMAX)
!      ARRHOU(1,1:NRMAX)=1.D0/RMJRHOU(1,1:NRMAX)**2
      ARRHOU(1,1:NRMAX)=1.D0/RRU(NTS)**2
      TTRHOU(1,1:NRMAX)=BBU(NTS)*RRU(NTS)

      KFID='RMINOR'
      CALL UF2DS(KFID,KUFDEV,KUFDCG,DR,TMU,FAS,AMP,NRMAX,1,0,MDLXP,IERR)
      RMNRHOU(1,1:NRMAX)=FAS(1:NRMAX)

      KFID='KAPPAR'
      CALL UF2DS(KFID,KUFDEV,KUFDCG,DR,TMU,FAS,AMP,NRMAX,0,1,MDLXP,IERR)
      IF(IERR.EQ.0) THEN
         RKPRHOU(1,1:NRMAX)=FAS(1:NRMAX)
      ELSE
         RKPRHOU(1,1:NRMAX)=RKAPU(NTS)
      ENDIF

      KFID='GRHO1'
      CALL UF2DS(KFID,KUFDEV,KUFDCG,DR,TMU,FAS,AMP,NRMAX,0,1,MDLXP,IERR)
      AR1RHOU(1,1:NRMAX)=FAS(1:NRMAX)

      KFID='GRHO2'
      CALL UF2DS(KFID,KUFDEV,KUFDCG,DR,TMU,FAS,AMP,NRMAX,0,1,MDLXP,IERR)
      AR2RHOU(1,1:NRMAX)=FAS(1:NRMAX)
      ABRHOU(1,1:NRMAX)=AR2RHOU(1,1:NRMAX)*ARRHOU(1,1:NRMAX)

      KFID='SURF'
      CALL UF2DS(KFID,KUFDEV,KUFDCG,DR,TMU,FAS,AMP,NRMAX,0,0,MDLXP,IERR)
      DVRHOU(1,1:NRMAX)=FAS(1:NRMAX)/AR1RHOU(1,1:NRMAX)

      KFID='VOLUME'
      CALL UFREAD2_TIME(KDIRX,KUFDEV,KUFDCG,KFID,RL,TL,F2,NRLMAX,NTXMAX,NRUM,NTUM,MDCHK,IERR)
      VOLAU(1:NTXMAX)=F2(1:NTXMAX,NRLMAX)

!     *****

      RR    = RRU(NTS)
      RA    = RAU(NTS)
      RIPS  = RIPU(NTS)
      RIPE  = RIPU(NTS)
      BB    = BBU(NTS)
      RKAP  = RKAPU(NTS)
      PHIA  = PHIAU(NTS)

!$$$      IF(NTXMAX1.EQ.0) THEN
!$$$         NTXMAX1=NTXMAX
!$$$         TMU1(1:NTXMAX)=TMU(1:NTXMAX)
!$$$      ENDIF
!$$$C
!$$$      DO NTX=1,NTXMAX1
!$$$         IF(ABS(TMU1(NTX)-TSLC).LE.1.D-5) THEN
!$$$            RR   = RRU(NTX)
!$$$            RA   = RAU(NTX)
!$$$            RIPS = RIPU(NTX)
!$$$            BB   = BBU(NTX)
!$$$            RKAP = RKAPU(NTX)
!$$$            PHIA = PHIAU(NTX)
!$$$            GOTO 2000
!$$$         ENDIF
!$$$      ENDDO
!$$$      WRITE(6,*) 'XX 1D UFILES NO SLICE TIME THE SAME AS ',
!$$$     &           'THAT OF 2D UFILES!'
!$$$      WRITE(6,*) '## THE PROCESS CONTINUES THROUGH USING THE TIMESPL ',
!$$$     &           'INTERPOLATION...'
!$$$      CALL TIMESPL(TSLC,RR  ,TMU1,RRU  ,NTXMAX1,NTUM,IERR)
!$$$      CALL TIMESPL(TSLC,RA  ,TMU1,RAU  ,NTXMAX1,NTUM,IERR)
!$$$      CALL TIMESPL(TSLC,RIPS,TMU1,RIPU ,NTXMAX1,NTUM,IERR)
!$$$      CALL TIMESPL(TSLC,BB  ,TMU1,BBU  ,NTXMAX1,NTUM,IERR)
!$$$      CALL TIMESPL(TSLC,RKAP,TMU1,RKAPU,NTXMAX1,NTUM,IERR)
!$$$      CALL TIMESPL(TSLC,PHIA,TMU1,PHIAU,NTXMAX1,NTUM,IERR)
!$$$ 2000 CONTINUE

      RETURN
      END SUBROUTINE TR_STEADY_UFILE

!     ***********************************************************

!           TIME EVOLUTIONAL UFILE DATA INPUT ROUTINE

!     ***********************************************************

      SUBROUTINE TR_TIME_UFILE

      USE TRCOMM, ONLY : ABRHOU, AJNBU, AJU, AR1RHOU, AR1RHOU, AR2RHOU, AR2RHOU, ARRHOU, BB, BBU, BPU, CNB, DR, DT, DVRHOU, &
     &                   KUFDCG, KUFDEV, MDLJQ, MDLXP, MDNI, MDPHIA, NRAMAX, NRMAX, NRMP, NRUM, NSM, NTUM, PBMU, PECU, PHIA, &
     &                   PHIAU, PICU, PN, PNBIU, PNBU, PNFU, PNFUA, PNFUA, PNS, PNSA, PNSU, PNSUA, POHU, POHU, PRLU, PT, PTS,&
     &                   PTSA, PTSU, PTSUA, PZ, QPU, RA, RAU, RHOA, RIPE, RIPS, RIPU, RKAP, RKAPU, RKPRHOU, RKPRHOU, RMJRHOU,&
     &                   RMNRHOU, RMNRHOU, RNFU, RNU, RNU_ORG, RR, RRU, RT, RTU, TTRHOU, VOLAU, WROTU, ZEFFU, ZEFFU_ORG
      USE TRCOM1, ONLY : KDIRX, NMCHK, NTAMAX, NTXMAX, NTXMAX1, TMU, TMU1
      IMPLICIT NONE
      INTEGER(4):: ICK, IERR, MDAMIN, MDBT, MDIP, MDKAPPA, MDPNBI, MDRGEO, NTAMAX1, NTX
      INTEGER(4):: MDCHK, MDSUM, NR, NRLMAX, NS
      REAL(8)   :: AMP, TMUMAX
      REAL(8),DIMENSION(NTUM)     :: PV, PV2, PV2A, PV3, PV3A, PVA, TL, ZEV, ZEVA
      REAL(8),DIMENSION(NRUM)     :: RL
      REAL(8),DIMENSION(NTUM,NRMP):: FAT
      REAL(8),DIMENSION(NTUM,NRUM):: F2
      CHARACTER(LEN=10):: KFID


      ICK=0
      TMUMAX=0.D0

      MDRGEO=0
      MDAMIN=0
      MDIP=0
      MDBT=0
      MDKAPPA=0
      MDPHIA=0
      MDPNBI=0

!     *** 1D VALUE ***

      KFID='RGEO'
      CALL UF1D(KFID,KUFDEV,KUFDCG,DT,TMU1,RRU ,NTAMAX1,NTXMAX1,TMUMAX,ICK,MDLXP,IERR)
      IF(IERR.EQ.1) THEN
         MDRGEO=IERR
         IERR=0
      ENDIF

      KFID='AMIN'
      CALL UF1D(KFID,KUFDEV,KUFDCG,DT,TMU1,RAU ,NTAMAX1,NTXMAX1,TMUMAX,ICK,MDLXP,IERR)
      IF(IERR.EQ.1) THEN
         MDAMIN=IERR
         IERR=0
      ENDIF

      KFID='IP'
      CALL UF1D(KFID,KUFDEV,KUFDCG,DT,TMU1,RIPU ,NTAMAX1,NTXMAX1,TMUMAX,ICK,MDLXP,IERR)
      IF(IERR.EQ.1) THEN
         MDIP=IERR
         IERR=0
      ELSE
         RIPU(1:NTXMAX1)=ABS(RIPU(1:NTXMAX1)*1.D-6)
      ENDIF

      KFID='BT'
      CALL UF1D(KFID,KUFDEV,KUFDCG,DT,TMU1,BBU  ,NTAMAX1,NTXMAX1,TMUMAX,ICK,MDLXP,IERR)
      IF(IERR.EQ.1) THEN
         MDBT=IERR
         IERR=0
      ELSE
         BBU(1:NTXMAX1)=ABS(BBU(1:NTXMAX1))
      ENDIF

      KFID='KAPPA'
      CALL UF1D(KFID,KUFDEV,KUFDCG,DT,TMU1,RKAPU,NTAMAX1,NTXMAX1,TMUMAX,ICK,MDLXP,IERR)
      IF(IERR.EQ.1) THEN
         MDKAPPA=IERR
         IERR=0
      ENDIF
!$$$      IF(IERR.EQ.1) THEN
!$$$         RKAPU(1:NTXMAX1)=1.D0
!$$$      ENDIF

      KFID='PHIA'
      CALL UF1D(KFID,KUFDEV,KUFDCG,DT,TMU1,PHIAU,NTAMAX1,NTXMAX1,TMUMAX,ICK,MDLXP,IERR)
      IF(IERR.EQ.1) THEN
         MDPHIA=IERR
         IERR=0
      ENDIF

      KFID='PNBI'
      CALL UF1D(KFID,KUFDEV,KUFDCG,DT,TMU1,PNBIU,NTAMAX1,NTXMAX1,TMUMAX,ICK,MDLXP,IERR)
      IF(IERR.EQ.1) THEN
         MDPNBI=IERR
         IERR=0
      ENDIF

      RR    = RRU(1)
      RA    = RAU(1)
      RIPS  = RIPU(1)
      RIPE  = RIPU(1)
      BB    = BBU(1)
      RKAP  = RKAPU(1)
      PHIA  = PHIAU(1)

!     *** 2D VALUE ***

      AMP=1.D-3
      KFID='TE'
      CALL UF2DTP(KFID,KUFDEV,KUFDCG,DR,DT,PV,PVA,TMU,FAT,AMP, &
     &          NTAMAX,NTXMAX,RHOA,NRAMAX,NRMAX,TMUMAX,0,ICK,MDLXP,IERR)
      RTU(1:NTXMAX,1:NRMAX,1)=FAT(1:NTXMAX,1:NRMAX)
      RT(1:NRMAX,1)=RTU(1,1:NRMAX,1)
      PT(1)=RT(1,1)
      PTSU(1:NTXMAX,1)=PV(1:NTXMAX)
      PTS(1)=PTSU(1,1)
      IF(RHOA.NE.1.D0) THEN
         PTSUA(1:NTXMAX,1)=PVA(1:NTXMAX)
         PTSA(1)=PTSUA(1,1)
      ENDIF

      KFID='TI'
      CALL UF2DTP(KFID,KUFDEV,KUFDCG,DR,DT,PV,PVA,TMU,FAT,AMP, &
     &          NTAMAX,NTXMAX,RHOA,NRAMAX,NRMAX,TMUMAX,0,ICK,MDLXP,IERR)
      DO NS=2,NSM
         RTU(1:NTXMAX,1:NRMAX,NS)=FAT(1:NTXMAX,1:NRMAX)
         RT(1:NRMAX,NS)=RTU(1,1:NRMAX,NS)
         PTSU(1:NTXMAX,NS)=PV(1:NTXMAX)
         PTS (NS)=PTSU(1,NS)
      ENDDO
      PT(2)=RT(1,2)
      PT(3)=RT(1,3)
      PT(4)=RT(1,2)
      IF(RHOA.NE.1.D0) THEN
         DO NS=2,NSM
            PTSUA(1:NTXMAX,NS)=PVA(1:NTXMAX)
            PTSA(NS)=PTSUA(1,NS)
         ENDDO
      ENDIF

      AMP=1.D-20
      KFID='NE'
      CALL UF2DTP(KFID,KUFDEV,KUFDCG,DR,DT,PV,PVA,TMU,FAT,AMP, &
     &          NTAMAX,NTXMAX,RHOA,NRAMAX,NRMAX,TMUMAX,0,ICK,MDLXP,IERR)
      RNU(1:NTXMAX,1:NRMAX,1)=FAT(1:NTXMAX,1:NRMAX)
      RNU(1:NTXMAX,1:NRMAX,2)=FAT(1:NTXMAX,1:NRMAX)
      RNU(1:NTXMAX,1:NRMAX,3)=1.D-7
      RNU(1:NTXMAX,1:NRMAX,4)=1.D-7
      RNU_ORG(1:NTXMAX,1:NRMAX,1)=FAT(1:NTXMAX,1:NRMAX)
      RNU_ORG(1:NTXMAX,1:NRMAX,2)=FAT(1:NTXMAX,1:NRMAX)
      ZEFFU(1:NTXMAX,1:NRMAX)=1.D0     ! in case of MDNI=0
      ZEFFU_ORG(1:NTXMAX,1:NRMAX)=1.D0 ! in case of MDNI=0
      PNSU(1:NTXMAX,1)=PV(1:NTXMAX)
      PNSU(1:NTXMAX,2)=PV(1:NTXMAX)
      PNSU(1:NTXMAX,3)=1.D-8
      PNSU(1:NTXMAX,4)=1.D-8
      PN(1)=RNU(1,1,1)
      PN(2)=RNU(1,1,2)
      PN(3)=1.D-7
      PN(4)=1.D-7
      PNS(1)=PNSU(1,1)
      PNS(2)=PNSU(1,2)
      PNS(3)=PNSU(1,3)
      PNS(4)=PNSU(1,4)
      IF(RHOA.NE.1.D0) THEN
         PNSUA(1:NTXMAX,1)=PVA(1:NTXMAX)
         PNSUA(1:NTXMAX,2)=PVA(1:NTXMAX)
         PNSUA(1:NTXMAX,3)=1.D-8
         PNSUA(1:NTXMAX,4)=1.D-8
         PNSA(1)=PNSUA(1,1)
         PNSA(2)=PNSUA(1,2)
         PNSA(3)=PNSUA(1,3)
         PNSA(4)=PNSUA(1,4)
      ENDIF

      KFID='NFAST'
      CALL UF2DTP(KFID,KUFDEV,KUFDCG,DR,DT,PNFU,PNFUA,TMU,FAT,AMP, &
     &          NTAMAX,NTXMAX,RHOA,NRAMAX,NRMAX,TMUMAX,0,ICK,MDLXP,IERR)
      DO NTX=1,NTXMAX
         DO NR=1,NRMAX
            IF(IERR.EQ.0.AND.FAT(NTX,NR).LE.0.D0) FAT(NTX,NR)=1.D-10
            RNFU(NTX,NR)=FAT(NTX,NR)
         ENDDO
      ENDDO

      KFID='NM1'
      CALL UF2DTP(KFID,KUFDEV,KUFDCG,DR,DT,PV,PVA,TMU,FAT,AMP, &
     &          NTAMAX,NTXMAX,RHOA,NRAMAX,NRMAX,TMUMAX,0,ICK,MDLXP,IERR)
      IF(NMCHK.EQ.0) THEN
         IF(MDNI.NE.0) THEN
            RNU(1:NTXMAX,1:NRMAX,2)=FAT(1:NTXMAX,1:NRMAX)
            IF(IERR.EQ.0) RNU_ORG(1:NTXMAX,1:NRMAX,2)=FAT(1:NTXMAX,1:NRMAX)
            RNU_ORG(1:NTXMAX,1:NRMAX,4)=0.D0
            PNSU(1:NTXMAX,2)=PV(1:NTXMAX)
            IF(RHOA.NE.1.D0) PNSUA(1:NTXMAX,2)=PVA(1:NTXMAX)
         ELSE
            IF(IERR.EQ.0) RNU_ORG(1:NTXMAX,1:NRMAX,2)=FAT(1:NTXMAX,1:NRMAX)
         ENDIF
      ELSE
         RNU_ORG(1:NTXMAX,1:NRMAX,4)=FAT(1:NTXMAX,1:NRMAX)
      ENDIF
      IF(NMCHK.GE.1) THEN
         KFID='NM2'
         CALL UF2DTP(KFID,KUFDEV,KUFDCG,DR,DT,PV2,PV2A,TMU,FAT,AMP, &
     &          NTAMAX,NTXMAX,RHOA,NRAMAX,NRMAX,TMUMAX,0,ICK,MDLXP,IERR)
         RNU_ORG(1:NTXMAX,1:NRMAX,2)=FAT(1:NTXMAX,1:NRMAX)
         IF(MDNI.NE.0) THEN
            RNU(1:NTXMAX,1:NRMAX,2)=FAT(1:NTXMAX,1:NRMAX)+RNU_ORG(1:NTXMAX,1:NRMAX,4)
            PNSU(1:NTXMAX,2)=PV(1:NTXMAX)+PV2(1:NTXMAX)
            IF(RHOA.NE.1.D0) PNSUA(1:NTXMAX,2)=PVA(1:NTXMAX)+PV2A(1:NTXMAX)
         ENDIF
      ENDIF
      IF(NMCHK.GE.2) THEN
         KFID='NM3'
         CALL UF2DTP(KFID,KUFDEV,KUFDCG,DR,DT,PV3,PV3A,TMU,FAT,AMP, &
     &          NTAMAX,NTXMAX,RHOA,NRAMAX,NRMAX,TMUMAX,0,ICK,MDLXP,IERR)
         RNU_ORG(1:NTXMAX,1:NRMAX,4)=RNU_ORG(1:NTXMAX,1:NRMAX,4)+FAT(1:NTXMAX,1:NRMAX)
         IF(MDNI.NE.0) THEN
            RNU(1:NTXMAX,1:NRMAX,2)=RNU(1:NTXMAX,1:NRMAX,2)+FAT(1:NTXMAX,1:NRMAX)
            PNSU(1:NTXMAX,2)=PNSU(1:NTXMAX,2)+PV3(1:NTXMAX)
            IF(RHOA.NE.1.D0) PNSUA(1:NTXMAX,2)=PNSUA(1:NTXMAX,2)+PV3A(1:NTXMAX)
         ENDIF
      ENDIF

      AMP=1.D0
      KFID='ZEFFR'
      CALL UF2DTP(KFID,KUFDEV,KUFDCG,DR,DT,ZEV,ZEVA,TMU,FAT,AMP, &
     &          NTAMAX,NTXMAX,RHOA,NRAMAX,NRMAX,TMUMAX,0,ICK,MDLXP,IERR)
      IF(MDNI.NE.0) ZEFFU(1:NTXMAX,1:NRMAX)=FAT(1:NTXMAX,1:NRMAX)
      ZEFFU_ORG(1:NTXMAX,1:NRMAX)=FAT(1:NTXMAX,1:NRMAX)

      AMP=1.D-20
      KFID='NIMP'
      CALL UF2DTP(KFID,KUFDEV,KUFDCG,DR,DT,PV,PVA,TMU,FAT,AMP, &
     &          NTAMAX,NTXMAX,RHOA,NRAMAX,NRMAX,TMUMAX,0,ICK,MDLXP,IERR)
      IF(MDNI.NE.0) THEN
         RNU(1:NTXMAX,1:NRMAX,3)=FAT(1:NTXMAX,1:NRMAX)
         RNU_ORG(1:NTXMAX,1:NRMAX,3)=FAT(1:NTXMAX,1:NRMAX)
         PNSU(1:NTXMAX,3)=PV(1:NTXMAX)
         IF(RHOA.NE.1.D0) PNSUA(1:NTXMAX,3)=PVA(1:NTXMAX)
      ELSE
         RNU_ORG(1:NTXMAX,1:NRMAX,3)=FAT(1:NTXMAX,1:NRMAX)
      ENDIF

      select case(MDNI)
      case(1)

      DO NTX=1,NTXMAX
         DO NR=1,NRMAX
            RNU(NTX,NR,2)=( (PZ(3)-ZEFFU(NTX,NR))*RNU(NTX,NR,1)-(PZ(3)-PZ(2))*PZ(2)*RNFU(NTX,NR))/(PZ(2)*(PZ(3)-PZ(2)))
            RNU(NTX,NR,3)=(PZ(2)-ZEFFU(NTX,NR))/(PZ(3)*(PZ(2)-PZ(3)))*RNU(NTX,NR,1)
         ENDDO
         PNSU(NTX,2)=( (PZ(3)-ZEV(NTX))*PNSU(NTX,1)-(PZ(3)-PZ(2))*PZ(2)*PNFU(NTX))/(PZ(2)*(PZ(3)-PZ(2)))
         PNSU(NTX,3)=(PZ(2)-ZEV(NTX))/(PZ(3)*(PZ(2)-PZ(3)))*PNSU(NTX,1)
         PNSU(NTX,4)=1.D-8
      ENDDO
      PN(2)=RNU(1,1,2)
      PN(3)=RNU(1,1,3)
      PN(4)=1.D-7
      PNS(2)=PNSU(1,2)
      PNS(3)=PNSU(1,3)
      PNS(4)=PNSU(1,4)
      IF(RHOA.NE.1.D0) THEN
         DO NTX=1,NTXMAX
            PNSUA(NTX,2)=( (PZ(3)-ZEVA(NTX))*PNSUA(NTX,1)-(PZ(3)-PZ(2))*PZ(2)*PNFUA(NTX))/(PZ(2)*(PZ(3)-PZ(2)))
            PNSUA(NTX,3)=(PZ(2)-ZEVA(NTX))/(PZ(3)*(PZ(2)-PZ(3)))*PNSUA(NTX,1)
            PNSUA(NTX,4)=1.D-8
         ENDDO
         PNSA(2)=PNSUA(1,2)
         PNSA(3)=PNSUA(1,3)
         PNSA(4)=PNSUA(1,4)
      ENDIF

      case(2)

      DO NTX=1,NTXMAX
         DO NR=1,NRMAX
            RNU(NTX,NR,3)=(RNU(NTX,NR,1)-(RNU(NTX,NR,2)+RNFU(NTX,NR))* PZ(2))/PZ(3)
            ZEFFU(NTX,NR)=PZ(2)*(PZ(2)-PZ(3))*(RNU(NTX,NR,2)+RNFU(NTX,NR))/ RNU(NTX,NR,1)+PZ(3)
         ENDDO
         PNSU(NTX,3)=(PNSU(NTX,1)-(PNSU(NTX,2)+PNFU(NTX))*PZ(2))/PZ(3)
         PNSU(NTX,4)=1.D-8
      ENDDO
      PN(2)=RNU(1,1,2)
      PN(3)=RNU(1,1,3)
      PN(4)=1.D-7
      PNS(2)=PNSU(1,2)
      PNS(3)=PNSU(1,3)
      PNS(4)=PNSU(1,4)
      IF(RHOA.NE.1.D0) THEN
         DO NTX=1,NTXMAX
            PNSUA(NTX,3)=(PNSUA(NTX,1)-(PNSUA(NTX,2)+PNFUA(NTX))*PZ(2))/PZ(3)
            PNSUA(NTX,4)=1.D-8
         ENDDO
         PNSA(2)=PNSUA(1,2)
         PNSA(3)=PNSUA(1,3)
         PNSA(4)=PNSUA(1,4)
      ENDIF

      case(3)

      DO NTX=1,NTXMAX
         DO NR=1,NRMAX
            RNU(NTX,NR,2)=( RNU(NTX,NR,1)-PZ(2)*RNFU(NTX,NR)-PZ(3)*RNU(NTX,NR,3))/PZ(2)
            ZEFFU(NTX,NR)=PZ(2)+PZ(3)*(PZ(3)-PZ(2))*(RNU(NTX,NR,3)/RNU(NTX,NR,1))
         ENDDO
         PNSU(NTX,2)=(PNSU(NTX,1)-PZ(2)*PNFU(NTX) -PZ(3)*PNSU(NTX,3))/PZ(2)
         PNSU(NTX,4)=1.D-8
      ENDDO
      PN(2)=RNU(1,1,2)
      PN(3)=RNU(1,1,3)
      PN(4)=1.D-7
      PNS(2)=PNSU(1,2)
      PNS(3)=PNSU(1,3)
      PNS(4)=PNSU(1,4)
      IF(RHOA.NE.1.D0) THEN
         DO NTX=1,NTXMAX
            PNSUA(NTX,2)=(PNSUA(NTX,1)-PZ(2)*PNFUA(NTX) -PZ(3)*PNSUA(NTX,3))/PZ(2)
            PNSUA(NTX,4)=1.D-8
         ENDDO
         PNSA(2)=PNSUA(1,2)
         PNSA(3)=PNSUA(1,3)
         PNSA(4)=PNSUA(1,4)
      ENDIF
      end select

!     check whether density developed is appropriate (positive) or not
      IF(MDNI.NE.0) THEN
      DO NTX=1,NTXMAX
         DO NR=1,NRMAX
            IF(RNU(NTX,NR,2).LE.0.D0.OR.RNU(NTX,NR,3).LE.0.D0) THEN
               WRITE(6,*) 'XX TR_STEADY_UFILE: DENSITY NEGATIVE: WRONG MDNI=',MDNI
               STOP
            ENDIF
         ENDDO
      ENDDO
      ENDIF

      AMP=1.D0
      KFID='PBEAM'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,PBMU,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)
      KFID='Q'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,QPU ,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,1,1,ICK,MDLXP,IERR)
      IF(IERR.NE.0.AND.MDLJQ.EQ.1) MDLJQ=0
      KFID='CURTOT'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,AJU ,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)
      IF(IERR.NE.0.AND.MDLJQ.EQ.0) MDLJQ=1
      KFID='CURNBI'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,AJNBU,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)
      KFID='BPOL'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,BPU,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,1,0,ICK,MDLXP,IERR)
      KFID='QNBIE'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,FAT,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)
      forall(NTX=1:NTXMAX,NR=1:NRMAX,FAT(NTX,NR) < 0.D0) FAT(NTX,NR) = 0.D0
      PNBU(1:NTXMAX,1:NRMAX,1) = FAT(1:NTXMAX,1:NRMAX)

      KFID='QNBII'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,FAT ,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)
      forall(NTX=1:NTXMAX,NR=1:NRMAX,FAT(NTX,NR) < 0.D0) FAT(NTX,NR) = 0.D0
      PNBU(1:NTXMAX,1:NRMAX,2) = FAT(1:NTXMAX,1:NRMAX)

      PICU(1:NTXMAX,1:NRMAX,1)=0.D0
      PICU(1:NTXMAX,1:NRMAX,2)=0.D0
      PECU(1:NTXMAX,1:NRMAX  )=0.D0
      POHU(1:NTXMAX,1:NRMAX  )=0.D0
      KFID='QICRHE'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,FAT ,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)
      forall(NTX=1:NTXMAX,NR=1:NRMAX,FAT(NTX,NR) < 0.D0) FAT(NTX,NR) = 0.D0
      PICU(1:NTXMAX,1:NRMAX,1) = FAT(1:NTXMAX,1:NRMAX)

      KFID='QICRHI'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,FAT ,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)
      forall(NTX=1:NTXMAX,NR=1:NRMAX,FAT(NTX,NR) < 0.D0) FAT(NTX,NR) = 0.D0
      PICU(1:NTXMAX,1:NRMAX,2) = FAT(1:NTXMAX,1:NRMAX)

      KFID='QECH'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,FAT ,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)
      forall(NTX=1:NTXMAX,NR=1:NRMAX,FAT(NTX,NR) < 0.D0) FAT(NTX,NR) = 0.D0
      PECU(1:NTXMAX,1:NRMAX) = FAT(1:NTXMAX,1:NRMAX)

      KFID='QRAD'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,PRLU,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)

      KFID='QOHM'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,POHU,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)

      KFID='VROT'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,WROTU,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,1,1,ICK,MDLXP,IERR)

!     *****

      MDSUM=MDRGEO+MDAMIN+MDIP+MDBT+MDKAPPA+MDPHIA+MDPNBI
      IF(MDSUM.NE.0) THEN
         NTXMAX1=NTXMAX
         IF(MDRGEO .NE.0) RRU  (1:NTXMAX1)=RR
         IF(MDAMIN .NE.0) RAU  (1:NTXMAX1)=RA
         IF(MDIP   .NE.0) RIPU (1:NTXMAX1)=RIPS
         IF(MDBT   .NE.0) BBU  (1:NTXMAX1)=BB
         IF(MDKAPPA.NE.0) RKAPU(1:NTXMAX1)=RKAP
         IF(MDPHIA .NE.0) PHIAU(1:NTXMAX1)=0.D0
         IF(MDPNBI .NE.0) PNBIU(1:NTXMAX1)=0.D0
         TMU1(1:NTXMAX1)=TMU(1:NTXMAX1)
      ENDIF

!     *** GEOMETRY FACTORS ***

      KFID='RMAJOR'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,RMJRHOU,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,1,1,ICK,MDLXP,IERR)
      DO NR=1,NRMAX
         ARRHOU(1:NTXMAX,NR)=1.D0/RRU(1:NTXMAX)**2
         TTRHOU(1:NTXMAX,NR)=RRU(1:NTXMAX)*BBU(1:NTXMAX)
      ENDDO
      IF(MDRGEO.NE.0) RRU(1:NTXMAX)=RMJRHOU(1:NTXMAX,1)

      KFID='RMINOR'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,RMNRHOU,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,1,0,ICK,MDLXP,IERR)
      IF(MDAMIN.NE.0) RAU(1:NTXMAX)=RMNRHOU(1:NTXMAX,NRMAX)

      KFID='KAPPAR'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,RKPRHOU,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)
      IF(MDKAPPA.NE.0) RKAPU(1:NTXMAX)=RKPRHOU(1:NTXMAX,NRMAX)

      KFID='GRHO1'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,AR1RHOU,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)

      KFID='GRHO2'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,AR2RHOU,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)
      ABRHOU(1:NTXMAX,1:NRMAX)=AR2RHOU(1:NTXMAX,1:NRMAX)*ARRHOU(1:NTXMAX,1:NRMAX)

      KFID='SURF'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,FAT,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,0,ICK,MDLXP,IERR)
      DVRHOU(1:NTXMAX,1:NRMAX)=FAT(1:NTXMAX,1:NRMAX)/AR1RHOU(1:NTXMAX,1:NRMAX)

      KFID='VOLUME'
      CALL UFREAD2_TIME(KDIRX,KUFDEV,KUFDCG,KFID,RL,TL,F2,NRLMAX,NTXMAX,NRUM,NTUM,MDCHK,IERR)
      VOLAU(1:NTXMAX)=F2(1:NTXMAX,NRLMAX)

      IF(NTAMAX.LT.NTAMAX1) NTAMAX=NTAMAX1
      RETURN
      END SUBROUTINE TR_TIME_UFILE


!     *****************************************************************

!           TIME EVOLUTIONAL UFILE DATA INPUT ROUTINE FOR TOPICS

!     *****************************************************************

      SUBROUTINE TR_TIME_UFILE_TOPICS

      USE TRCOMM, ONLY : ABRHOU, AJBSU, AJU, AR1RHOU, AR2RHOU, ARRHOU, BB, BBU, DR, DT, DVRHOU, EPSRHO, KUFDCG, KUFDEV, &
     &                   MDLXP, MDPHIA, NRAMAX, NRMAX, NRMP, NRUM, NTUM, PECU, PHIA, PI, PICU, PNBU, PNSU, PNSUA, POHU, &
     &                   PRLU, PZ, QPU, RA, RAU, RG, RHOA, RIPE, RIPS, RIPU, RKAP, RKAPU, RKPRHO, RMJRHOU, RMNRHOU, RNU, &
     &                   RR, RRU, RTU, TTRHOU, VOLAU, ZEFFU
      USE TRCOM1, ONLY : NTAMAX, NTXMAX, NTXMAX1, TMU, TMU1
      IMPLICIT NONE
      INTEGER(4):: ICK, IERR, MDAMIN, MDBT, MDIP, MDKAPPA, MDRGEO, MDVOL, NTAMAX1, NTX
      INTEGER(4):: MDALL, MDCHK, NR, NRLMAX
      REAL(8)   :: AMP, TMUMAX
      REAL(8),DIMENSION(NTUM)     :: PV, PVA, TL, VOL
      REAL(8),DIMENSION(NRUM)     :: RL
      REAL(8),DIMENSION(NTUM,NRMP):: FAT
      REAL(8),DIMENSION(NTUM,NRUM):: F2
      CHARACTER(LEN=10):: KFID
      CHARACTER(LEN=80):: KDIRX


      ICK=0
      TMUMAX=0.D0

      MDRGEO=0
      MDAMIN=0
      MDIP=0
      MDBT=0
      MDKAPPA=0
      MDVOL=0
      MDPHIA=0

!     *** 1D VALUE ***

      KFID='RGEO'
      CALL UF1D(KFID,KUFDEV,KUFDCG,DT,TMU1,RRU  ,NTAMAX1,NTXMAX1,TMUMAX,ICK,MDLXP,IERR)
      MDRGEO=IERR

      KFID='AMIN'
      CALL UF1D(KFID,KUFDEV,KUFDCG,DT,TMU1,RAU  ,NTAMAX1,NTXMAX1,TMUMAX,ICK,MDLXP,IERR)
      MDAMIN=IERR

      KFID='IP'
      CALL UF1D(KFID,KUFDEV,KUFDCG,DT,TMU1,RIPU ,NTAMAX1,NTXMAX1,TMUMAX,ICK,MDLXP,IERR)
      RIPU(1:NTXMAX1)=ABS(RIPU(1:NTXMAX1)*1.D-6)
      MDIP=IERR

      KFID='BT'
      CALL UF1D(KFID,KUFDEV,KUFDCG,DT,TMU1,BBU  ,NTAMAX1,NTXMAX1,TMUMAX,ICK,MDLXP,IERR)
      MDBT=IERR

      KFID='KAPPA'
      CALL UF1D(KFID,KUFDEV,KUFDCG,DT,TMU1,RKAPU,NTAMAX1,NTXMAX1,TMUMAX,ICK,MDLXP,IERR)
      MDKAPPA=IERR

      KFID='VOL'
      CALL UF1D(KFID,KUFDEV,KUFDCG,DT,TMU1,VOL, NTAMAX1,NTXMAX1,TMUMAX,ICK,MDLXP,IERR)
      MDVOL=IERR

      RR    = RRU(1)
      RA    = RAU(1)
      RIPS  = RIPU(1)
      RIPE  = RIPU(1)
      BB    = BBU(1)
      RKAP  = RKAPU(1)
      PHIA  = 0.D0

!     *** 2D VALUE ***

      AMP=1.D-3
      KFID='TE'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,FAT,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)
      RTU(1:NTXMAX,1:NRMAX,1)=FAT(1:NTXMAX,1:NRMAX)

      KFID='TI'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,FAT,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)
      RTU(1:NTXMAX,1:NRMAX,2)=FAT(1:NTXMAX,1:NRMAX)
      RTU(1:NTXMAX,1:NRMAX,3)=FAT(1:NTXMAX,1:NRMAX)

      AMP=1.D-20
      KFID='NE'
      CALL UF2DTP(KFID,KUFDEV,KUFDCG,DR,DT,PV,PVA,TMU,FAT,AMP, &
     &          NTAMAX,NTXMAX,RHOA,NRAMAX,NRMAX,TMUMAX,0,ICK,MDLXP,IERR)
      RNU(1:NTXMAX,1:NRMAX,1)=FAT(1:NTXMAX,1:NRMAX)
!      RNU(1:NTXMAX,1:NRMAX,2)=FAT(1:NTXMAX,1:NRMAX)
      PNSU (1:NTXMAX,1)=PV (1:NTXMAX)
!      PNSU (1:NTXMAX,2)=PV (1:NTXMAX)
      IF(RHOA.NE.1.D0) THEN
         PNSUA(1:NTXMAX,1)=PVA(1:NTXMAX)
!         PNSUA(1:NTXMAX,2)=PVA(1:NTXMAX)
      ENDIF

      KFID='NIMP'
      CALL UF2DTP(KFID,KUFDEV,KUFDCG,DR,DT,PV,PVA,TMU,FAT,AMP,NTAMAX,NTXMAX,RHOA,NRAMAX,NRMAX,TMUMAX,0,ICK,MDLXP,IERR)
      RNU(1:NTXMAX,1:NRMAX,2)=(RNU(1:NTXMAX,1:NRMAX,1)-PZ(3)*FAT(1:NTXMAX,1:NRMAX))/PZ(2)
      RNU(1:NTXMAX,1:NRMAX,3)=FAT(1:NTXMAX,1:NRMAX)
      PNSU(1:NTXMAX,2)=(PNSU(1:NTXMAX,1)-PZ(3)*PV(1:NTXMAX))/PZ(2)
      PNSU(1:NTXMAX,3)=PV(1:NTXMAX)
      IF(RHOA.NE.1.D0) THEN
         PNSUA(1:NTXMAX,2)=(PNSUA(1:NTXMAX,1)-PZ(3)*PVA(1:NTXMAX))/PZ(2)
         PNSUA(1:NTXMAX,3)=PVA(1:NTXMAX)
      ENDIF

      KFID='NM1'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,FAT,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)
      RNU(1:NTXMAX,1:NRMAX,4)=FAT(1:NTXMAX,1:NRMAX)

      AMP=1.D0
      KFID='ZEFFR'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,FAT,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,1,1,ICK,MDLXP,IERR)
      ZEFFU(1:NTXMAX,1:NRMAX)=FAT(1:NTXMAX,1:NRMAX)

      KFID='Q'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,QPU,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,1,1,ICK,MDLXP,IERR)
      KFID='CURTOT'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,AJU,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)
      IF(KUFDEV.EQ.'X'.AND.KUFDCG.EQ.'14') THEN
         KFID='CURBS'
         CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,AJBSU,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)
      ENDIF

      KFID='QNBIE'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,FAT,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)
      forall(NTX=1:NTXMAX,NR=1:NRMAX,FAT(NTX,NR) < 0.D0) FAT(NTX,NR) = 0.D0
      PNBU(1:NTXMAX,1:NRMAX,1) = FAT(1:NTXMAX,1:NRMAX)

      KFID='QNBII'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,FAT,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)
      forall(NTX=1:NTXMAX,NR=1:NRMAX,FAT(NTX,NR) < 0.D0) FAT(NTX,NR) = 0.D0
      PNBU(1:NTXMAX,1:NRMAX,2) = FAT(1:NTXMAX,1:NRMAX)

      PICU(1:NTXMAX,1:NRMAX,1)=0.D0
      PICU(1:NTXMAX,1:NRMAX,2)=0.D0
      PECU(1:NTXMAX,1:NRMAX  )=0.D0
      POHU(1:NTXMAX,1:NRMAX  )=0.D0
!      KFID='QICRHE'
      KFID='QLHE'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,FAT,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)
      forall(NTX=1:NTXMAX,NR=1:NRMAX,FAT(NTX,NR) < 0.D0) FAT(NTX,NR) = 0.D0
      PICU(1:NTXMAX,1:NRMAX,1) = FAT(1:NTXMAX,1:NRMAX)

!      KFID='QICRHI'
      KFID='QLHI'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,FAT,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)
      forall(NTX=1:NTXMAX,NR=1:NRMAX,FAT(NTX,NR) < 0.D0) FAT(NTX,NR) = 0.D0
      PICU(1:NTXMAX,1:NRMAX,2) = FAT(1:NTXMAX,1:NRMAX)

      KFID='QECH'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,FAT,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)
      forall(NTX=1:NTXMAX,NR=1:NRMAX,FAT(NTX,NR) < 0.D0) FAT(NTX,NR) = 0.D0
      PECU(1:NTXMAX,1:NRMAX) = FAT(1:NTXMAX,1:NRMAX)

      KFID='QRAD'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,PRLU,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)

      KFID='QOHM'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,POHU,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)

!     *** GEOMETRY FACTORS ***

      KFID='RMAJOR'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,RMJRHOU,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,1,1,ICK,MDLXP,IERR)

      KFID='RMINOR'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,RMNRHOU,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,1,0,ICK,MDLXP,IERR)

      KFID='GRHO1'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,AR1RHOU,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)

      KFID='GRHO2'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,AR2RHOU,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)

      KFID='VOLUME'
      CALL UFREAD2_TIME(KDIRX,KUFDEV,KUFDCG,KFID,RL,TL,F2,NRLMAX,NTXMAX,NRUM,NTUM,MDCHK,IERR)
      VOLAU(1:NTXMAX)=F2(1:NTXMAX,NRLMAX)

      KFID='VRO'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,DVRHOU,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,0,ICK,MDLXP,IERR)

      KFID='AAT'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,ARRHOU,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)

      KFID='HDT'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,FAT,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)
      TTRHOU(1:NTXMAX,1:NRMAX)=FAT(1:NTXMAX,1:NRMAX)/ARRHOU(1:NTXMAX,1:NRMAX)

      KFID='CKT'
      CALL UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TMU,FAT,AMP,NTAMAX,NTXMAX,NRMAX,TMUMAX,0,1,ICK,MDLXP,IERR)
      ABRHOU(1:NTXMAX,1:NRMAX)=FAT(1:NTXMAX,1:NRMAX)/DVRHOU(1:NTXMAX,1:NRMAX)**2

      DO NR=1,NRMAX
         RG(NR)    =DBLE(NR)*DR
      ENDDO
      EPSRHO(1:NRMAX)=RMNRHOU(1,1:NRMAX)/RMJRHOU(1,1:NRMAX)
      RKPRHO(1:NRMAX)=RKAP

!     *** 1D VALUE ***

      MDALL=MDRGEO+MDAMIN+MDIP+MDBT+MDKAPPA
      IF(MDALL.NE.0) THEN
         NTXMAX1=NTXMAX
         IF(MDRGEO.NE.0)  RRU(1:NTXMAX1)=RR
         IF(MDAMIN.NE.0)  RAU(1:NTXMAX1)=RA
         IF(MDIP.NE.0)    RIPU(1:NTXMAX1)=RIPS
         IF(MDBT.NE.0)    BBU(1:NTXMAX1)=BB
         IF(MDKAPPA.NE.0) RKAPU(1:NTXMAX1)=RKAP
         IF(MDVOL.NE.0)   VOL(1:NTXMAX1)=PI*RKAP*RA**2*2.D0*PI*RR
         TMU1(1:NTXMAX1)=TMU(1:NTXMAX1)
      ENDIF

!     ***

      RETURN
      END SUBROUTINE TR_TIME_UFILE_TOPICS


!     ***********************************************************

!           1D UFILE READER

!     ***********************************************************

!     input:

!     KFID        : Variable Name
!     DT          : Time Step Width
!     NTLMAX      : Maximum Time Step for TASK/TR
!     TLMAX       : Maximum Time for TASK/TR
!     ICK         : Check Indicator of UFILE Consistency
!     MDLXP       : Select UFILE or MDSplus

!     output:

!     TL(NTUM)    : Total Time Data (The Number of DT)
!     F1(NTUM)    : Functional Values
!     IERR        : Error Indicator

!     *****************************************************

      SUBROUTINE UF1D(KFID,KUFDEV,KUFDCG,DT,TL,F1,NTLMAX,NTXMAX,TLMAX,ICK,MDLXP,IERR)

      USE TRCOMM, ONLY : NTUM
      USE TRCOM1, ONLY : KDIRX
      IMPLICIT NONE
      CHARACTER(LEN=10),INTENT(IN)   :: KFID
      CHARACTER(LEN=80),INTENT(INOUT):: KUFDEV,KUFDCG
      REAL(8)          ,INTENT(IN)   :: DT
      REAL(8)          ,INTENT(INOUT):: TLMAX
      REAL(8),DIMENSION(NTUM),INTENT(OUT):: TL, F1
      INTEGER(4)       ,INTENT(IN)   :: NTXMAX, MDLXP
      INTEGER(4)       ,INTENT(INOUT):: NTLMAX, ICK
      INTEGER(4)       ,INTENT(OUT)  :: IERR
      INTEGER(4):: MDCHK, NTX


      IF(MDLXP.EQ.0) THEN
         CALL UFREAD_TIME(KDIRX,KUFDEV,KUFDCG,KFID,TL,F1,NTXMAX,NTUM,MDCHK,IERR)
      ELSE
         CALL IPDB_MDS1(KUFDEV,KUFDCG,KFID,NTUM,TL,F1,NTXMAX,IERR)
      ENDIF
      IF(IERR.NE.0) THEN
         WRITE(6,'(A13,A10,A14)') '## UF1D: NO "',KFID,'" FILE EXISTS.'
         RETURN
      ENDIF
!
!     Time mesh normalization (from t=a to t=b -> t=0 to t=b-a)
!
      TL(2:NTXMAX)=(TL(2:NTXMAX)-TL(1))
      TL(1)=0.D0
      IF(ICK.NE.0) THEN
         IF(ICK.EQ.2.AND.TLMAX.EQ.0.D0) GOTO 1000
         IF(TLMAX.NE.TL(NTXMAX)) THEN
            WRITE(6,*) 'XX UF1D:',KFID,'UFILE HAS AN ERROR!'
            WRITE(6,*) TLMAX,TL(NTXMAX)
            STOP
         ENDIF
      ENDIF
 1000 CONTINUE
      TLMAX=TL(NTXMAX)
      NTLMAX=INT(DINT(TL(NTXMAX)*1.D2)*1.D-2/DT)
      IF(ICK.NE.2) ICK=1

      RETURN
      END SUBROUTINE UF1D

!     *** ROUTINE FOR TEXT ***

      SUBROUTINE UF1DT(KFID,KUFDEV,KUFDCG,NTS,FOUT,AMP,MDLXP,IERR)

      USE TRCOMM, ONLY : NTUM
      USE TRCOM1, ONLY : KDIRX
      IMPLICIT NONE
      CHARACTER(LEN=10),INTENT(IN)   :: KFID
      CHARACTER(LEN=80),INTENT(INOUT):: KUFDEV,KUFDCG
      INTEGER(4)       ,INTENT(IN)   :: NTS, MDLXP
      INTEGER(4)       ,INTENT(OUT)  :: IERR
      REAL(8)          ,INTENT(IN)   :: AMP
      REAL(8)          ,INTENT(OUT)  :: FOUT
      INTEGER(4)             :: NTXMAX, MDCHK
      REAL(8),DIMENSION(NTUM):: F1, TL


      IF(MDLXP.EQ.0) THEN
         CALL UFREAD_TIME(KDIRX,KUFDEV,KUFDCG,KFID,TL,F1,NTXMAX,NTUM,MDCHK,IERR)
      ELSE
         CALL IPDB_MDS1(KUFDEV,KUFDCG,KFID,NTUM,TL,F1,NTXMAX,IERR)
      ENDIF
      IF(IERR.NE.0) THEN
         WRITE(6,'(A14,A10,A14)') '## UF1DG: NO "',KFID,'" FILE EXISTS.'
         RETURN
      ENDIF

      IF(IERR.EQ.1) THEN
         FOUT=0.D0
      ELSE
         FOUT=F1(NTS)*AMP
      ENDIF

      RETURN
      END SUBROUTINE UF1DT

!     *** ROUTINE FOR GRAPHIC ***

      SUBROUTINE UF1DG(KFID,KUFDEV,KUFDCG,GTL,TL,FOUT,AMP,NINMAX,MDLXP,IERR)

      USE TRCOMM, ONLY : NTUM
      USE TRCOM1, ONLY : KDIRX
      IMPLICIT NONE
      CHARACTER(LEN=10),INTENT(IN)   :: KFID
      CHARACTER(LEN=80),INTENT(INOUT):: KUFDEV, KUFDCG
      REAL(8),          INTENT(IN)   :: AMP
      REAL(8),DIMENSION(NTUM),INTENT(INOUT):: TL, FOUT
      REAL(4),DIMENSION(NTUM),INTENT(IN)   :: GTL
      INTEGER(4),             INTENT(IN)   :: NINMAX, MDLXP
      INTEGER(4),             INTENT(OUT)  :: IERR
      INTEGER(4):: NTXMAX, MDCHK, NTX, NIN, IERRL
      REAL(8)   :: TLN, F0
      REAL(8),DIMENSION(NTUM):: F1(NTUM)


      IF(MDLXP.EQ.0) THEN
         CALL UFREAD_TIME(KDIRX,KUFDEV,KUFDCG,KFID,TL,F1,NTXMAX,NTUM,MDCHK,IERR)
      ELSE
         CALL IPDB_MDS1(KUFDEV,KUFDCG,KFID,NTUM,TL,F1,NTXMAX,IERR)
      ENDIF
      IF(IERR.NE.0) THEN
         WRITE(6,'(A14,A10,A14)') '## UF1DG: NO "',KFID,'" FILE EXISTS.'
         RETURN
      ENDIF
!
!     Time mesh normalization (from t=a to t=b -> t=0 to t=b-a)
!
      TL(2:NTXMAX)=(TL(2:NTXMAX)-TL(1))
      TL(1)=0.D0
      DO NIN=1,NINMAX
         TLN=DBLE(GTL(NIN))
         CALL TIMESPL(TLN,F0,TL,F1,NTXMAX,NTUM,IERRL)
!         IF(IERRL.NE.0) WRITE(6,600) "XX UF1DG: TIMESPL ",KFID,": IERR=",IERR
         FOUT(NIN)=F0
      ENDDO
!
      IF(IERR.EQ.1) THEN
         FOUT(1:NINMAX)=0.D0
      ELSE
         FOUT(1:NINMAX)=FOUT(1:NINMAX)*AMP
      ENDIF

 600  FORMAT(' ',A18,A10,A7,I2)
      RETURN
      END SUBROUTINE UF1DG

!     ***********************************************************

!           2D UFILE READER

!     ***********************************************************

!     *** FOR STEADY STATE SIMULATION ***

!     input:

!     KFID        : Variable Name
!     DR          : Radial Step Width
!     AMP         : Amplitude Factor of FOUT
!     NRMAX       : Radial Node Number
!     NSW         : Mesh Selector (0:RM, 1:RG)
!     ID          : Boundary Condition in the center and/or edge for Spline
!     MDLXP       : Select UFILE or MDSplus

!     output:

!     TL(NTUM)    : Total Time Data (The Number of DT)
!     FOUT(NRMP)  : Functional Values
!     IERR        : Error Indicator

!     *****************************************************

      SUBROUTINE UF2DS(KFID,KUFDEV,KUFDCG,DR,TL,FOUT,AMP,NRMAX,NSW,ID,MDLXP,IERR)

      USE TRCOMM, ONLY : NRMP, NRUM, NTUM
      USE TRCOM1, ONLY : KDIRX, NTXMAX
      IMPLICIT NONE
      CHARACTER(LEN=10),INTENT(IN):: KFID
      CHARACTER(LEN=80),INTENT(INOUT):: KUFDEV, KUFDCG
      REAL(8),DIMENSION(NTUM),INTENT(OUT):: TL
      REAL(8),DIMENSION(NRMP),INTENT(OUT):: FOUT
      REAL(8),INTENT(IN) :: DR,  AMP
      INTEGER(4),INTENT(IN):: NRMAX, NSW, ID, MDLXP
      INTEGER(4),INTENT(OUT):: IERR

      INTEGER(4):: IERRP, IERRS, MDCHK, NRL, NRLMAX, NTX
      REAL(8)   :: F0, FCTR, RSL
      REAL(8),DIMENSION(NRUM):: RL
      REAL(8),DIMENSION(4,NRUM):: U
      REAL(8),DIMENSION(NTUM,NRUM):: F2


      IF(MDLXP.EQ.0) THEN
         CALL UFREAD2_TIME(KDIRX,KUFDEV,KUFDCG,KFID,RL,TL,F2,NRLMAX,NTXMAX,NRUM,NTUM,MDCHK,IERR)
      ELSE
         CALL IPDB_MDS2(KUFDEV,KUFDCG,KFID,NRUM,NTUM,RL,TL,F2,NRLMAX,NTXMAX,IERR)
      ENDIF

      IF(KUFDEV.EQ.'jet'.AND.(KUFDCG.EQ.'35156'.OR.KUFDCG.EQ.'35171') &
     &     .AND.KFID.EQ.'GRHO1') THEN
         DO NTX=1,NTXMAX
            F2(NTX,2)=FCTR(RL(3),RL(4),F2(NTX,3),F2(NTX,4))
            F2(NTX,1)=FCTR(RL(2),RL(3),F2(NTX,2),F2(NTX,3))
         ENDDO
      ENDIF
      IERRP=IERR
      CALL PRETREAT0(KFID,RL,TL,F2,U,NRLMAX,NTXMAX,ID,IERRP)
!
!     Error check in case of a single radial point
!
      IF(IERRP.NE.0.AND.NRLMAX.LE.1) THEN
         FOUT(1:NRMAX)=0.D0
         IERR=1
         IF(IERRP.LT.0) IERR=IERRP
         RETURN
      ENDIF
!
!     Calculate values suitable for arbitrary radial mesh using spline
!
      DO NRL=1,NRMAX
         IF(NSW.EQ.0) THEN
            RSL=(DBLE(NRL)-0.5D0)*DR
         ELSEIF(NSW.EQ.1) THEN
            RSL= DBLE(NRL)       *DR
         ENDIF
         CALL SPL1DF(RSL,F0,RL,U,NRLMAX,IERRS)
         IF(IERRS.NE.0) WRITE(6,600) "XX UF2DS: SPL1DF ",KFID,": IERRS=",IERRS
         FOUT(NRL)=F0*AMP
      ENDDO

 600  FORMAT(' ',A17,A10,A7,I2)
      RETURN
      END SUBROUTINE UF2DS

!     *****************************************************

!     input:

!     KFID        : Variable name
!     DR          : Radial Step Width
!     AMP         : Amplitude Factor of FOUT
!     RHOA        : Normalized Radius at Arbitrary Surface
!     NRAMAX      : Radial Node Number Corresponding to RHOA
!     NRMAX       : Radial Node Number
!     MDLXP       : Select UFILE or MDSplus

!     output:

!     PV          : Surface(Peripheral) Functional Value
!     PVS         : Surface(Peripheral) Functional Value
!                   Corresponding to RHOA
!     FOUT(NRMP)  : Functional Values
!     IERR        : Error Indicator

!     *****************************************************

      SUBROUTINE UF2DSP(KFID,KUFDEV,KUFDCG,DR,PV,PVA,FOUT,AMP,RHOA,NRAMAX,NRMAX,MDLXP,IERR)

      USE TRCOMM, ONLY : NRMP, NRUM, NTUM
      USE TRCOM1, ONLY : KDIRX
      IMPLICIT NONE
      CHARACTER(LEN=10), INTENT(IN):: KFID
      CHARACTER(LEN=80), INTENT(INOUT):: KUFDEV, KUFDCG
      REAL(8) ,          INTENT(IN)  :: DR, AMP, RHOA
      REAL(8) ,          INTENT(OUT)  :: PV, PVA
      REAL(8),DIMENSION(NRMP), INTENT(OUT)::FOUT
      INTEGER(4),        INTENT(IN):: NRAMAX, NRMAX, MDLXP
      INTEGER(4),        INTENT(OUT):: IERR
      INTEGER(4):: IERRP, MDCHK, NRL, NRLMAX, NTXMAX
      REAL(8)   :: F0, RGN, RMN
      REAL(8),DIMENSION(NRUM):: RL
      REAL(8),DIMENSION(NTUM):: TL
      REAL(8),DIMENSION(NTUM,NRUM):: F2
      REAL(8),DIMENSION(4,NRUM):: U


      IF(MDLXP.EQ.0) THEN
         CALL UFREAD2_TIME(KDIRX,KUFDEV,KUFDCG,KFID,RL,TL,F2,NRLMAX,NTXMAX,NRUM,NTUM,MDCHK,IERR)
      ELSE
         CALL IPDB_MDS2(KUFDEV,KUFDCG,KFID,NRUM,NTUM,RL,TL,F2,NRLMAX,NTXMAX,IERR)
      ENDIF
      IERRP=IERR
      CALL PRETREAT0(KFID,RL,TL,F2,U,NRLMAX,NTXMAX,1,IERRP)
!
!     Error check in case of a single radial point
!
      IF(IERRP.NE.0.OR.NRLMAX.LE.1) THEN
         FOUT(1:NRMAX)=0.D0
         RETURN
!200607   IERR=1
!200607   IF(IERR.LT.0) IERR=IERRP
      ENDIF
!
!     Calculate values suitable for arbitrary radial mesh using spline
!
      DO NRL=1,NRMAX
         RMN=(DBLE(NRL)-0.5D0)*DR
         CALL SPL1DF(RMN,F0,RL,U,NRLMAX,IERR)
         IF(IERR.NE.0) WRITE(6,600) "XX UF2DSP: SPL1DF ",KFID,": IERR=",IERR
         FOUT(NRL)=F0*AMP
      ENDDO
!
!     Edge values
!
      RGN=DBLE(NRMAX)*DR
      CALL SPL1DF(RGN,F0,RL,U,NRLMAX,IERR)
      IF(IERR.NE.0) WRITE(6,600) "XX UF2DSP: SPL1DF ",KFID,": IERR=",IERR
      PV=F0*AMP
      IF(RHOA.NE.1.D0) THEN
         RGN=DBLE(NRAMAX)*DR
         CALL SPL1DF(RGN,F0,RL,U,NRLMAX,IERR)
         IF(IERR.NE.0) WRITE(6,600) "XX UF2DSP: SPL1DF ",KFID,": IERR=",IERR
         PVA=F0*AMP
      ENDIF

 600  FORMAT(' ',A18,A10,A7,I2)
      RETURN
      END SUBROUTINE UF2DSP

!     *** FOR TIME EVOLUTION SIMULATION ***

!     input:

!     KFID        : Variable name
!     DR          : Radial Step Width
!     DT          : Time Step Width
!     AMP         : Amplitude Factor of FOUT
!     NRMAX       : Radial Node Number
!     TLMAX       : Maximum Time
!     NSW         : Mesh Selector (0:RM, 1:RG)
!     ID          : Boundary Condition in the center and/or edge for Spline
!     ICK         : Check Indicator
!     MDLXP       : Select UFILE or MDSplus

!     output:

!     TL(NTUM)    : Total Time Data (The Number of DT)
!     FOUT(NRMP)  : Functional Values
!     NTAMAX      : Maximum Time Step Number Corresponding to RHOA
!     IERR        : Error Indicator

!     *****************************************************

      SUBROUTINE UF2DT(KFID,KUFDEV,KUFDCG,DR,DT,TL,FOUT,AMP,NTLMAX,NTXMAX,NRMAX,TLMAX,NSW,ID,ICK,MDLXP,IERR)

      USE TRCOMM, ONLY : NRMP, NRUM, NTUM
      USE TRCOM1, ONLY : KDIRX
      IMPLICIT NONE
      CHARACTER(LEN=10),           INTENT(IN):: KFID
      CHARACTER(LEN=80),           INTENT(INOUT):: KUFDEV, KUFDCG
      REAL(8),DIMENSION(NTUM),     INTENT(OUT):: TL
      REAL(8),DIMENSION(NTUM,NRMP),INTENT(OUT):: FOUT
      REAL(8),                     INTENT(IN):: DR, DT, AMP
      REAL(8),                     INTENT(INOUT):: TLMAX
      INTEGER(4),                  INTENT(IN):: NTXMAX, NRMAX, NSW, ID, MDLXP
      INTEGER(4),                  INTENT(INOUT):: ICK
      INTEGER(4),                  INTENT(OUT):: NTLMAX, IERR
      INTEGER(4):: MDCHK, NRL, NRLMAX, NRX, NTX
      REAL(8)   :: F0, RSL
      REAL(8),DIMENSION(NRUM):: RL
      REAL(8),DIMENSION(NTUM,NRUM):: F2
      REAL(8),DIMENSION(4,NRUM):: U
      REAL(8),DIMENSION(NRUM):: DERIV, TMP0

      IF(MDLXP.EQ.0) THEN
         CALL UFREAD2_TIME(KDIRX,KUFDEV,KUFDCG,KFID,RL,TL,F2,NRLMAX,NTXMAX,NRUM,NTUM,MDCHK,IERR)
      ELSE
         CALL IPDB_MDS2(KUFDEV,KUFDCG,KFID,NRUM,NTUM,RL,TL,F2,NRLMAX,NTXMAX,IERR)
      ENDIF
!
!     Error check in case of a single radial point
!
      IF(IERR.NE.0.AND.NRLMAX.LE.1) THEN
         FOUT(1:NTUM,1:NRMP)=0.D0
         IF(NRLMAX.LE.1) IERR=1
         RETURN
      ENDIF
      DERIV(1)=0.D0
      DERIV(NRLMAX)=0.D0
!
!     Time mesh normalization (from t=a to t=b -> t=0 to t=b-a)
!
      TL(2:NTXMAX)=(TL(2:NTXMAX)-TL(1))
      TL(1)=0.D0
      IF(ICK.NE.0) THEN
         IF(ICK.EQ.2.AND.TLMAX.EQ.0.D0) GOTO 1000
         IF(TLMAX.NE.TL(NTXMAX)) THEN
            WRITE(6,*) 'XX UF2DT:',KFID,'UFILE HAS AN ERROR!'
            WRITE(6,*) TLMAX,TL(NTXMAX)
            STOP
         ENDIF
      ENDIF
 1000 CONTINUE
      TLMAX=TL(NTXMAX)
      NTLMAX=INT(DINT(TL(NTXMAX)*1.D2)*1.D-2/DT)
      IF(ICK.NE.2) ICK=1
!
!     Calculate values suitable for arbitrary radial mesh using spline
!
      DO NTX=1,NTXMAX
         TMP0(1:NRLMAX)=F2(NTX,1:NRLMAX)
         CALL SPL1D(RL,TMP0,DERIV,U,NRLMAX,ID,IERR)
         DO NRL=1,NRMAX
            IF(NSW.EQ.0) THEN
               RSL=(DBLE(NRL)-0.5D0)*DR
            ELSEIF(NSW.EQ.1) THEN
               RSL= DBLE(NRL)       *DR
            ENDIF
            CALL SPL1DF(RSL,F0,RL,U,NRLMAX,IERR)
            IF(IERR.NE.0) WRITE(6,600) "XX TRFILE: SPL1DF ",KFID,": IERR=",IERR
            FOUT(NTX,NRL)=F0*AMP
         ENDDO
      ENDDO

 600  FORMAT(' ',A18,A10,A7,I2)
      RETURN
      END SUBROUTINE UF2DT

!     *** FOR THE VARIABLE WE'D LIKE TO USE PERIPHERAL VALUES ***

      SUBROUTINE UF2DTP(KFID,KUFDEV,KUFDCG,DR,DT,PV,PVA,TL,FOUT,AMP, &
     &                  NTLMAX,NTXMAX,RHOA,NRAMAX,NRMAX,TLMAX,NSW,ICK,MDLXP,IERR)

      USE TRCOMM, ONLY : NRMP, NRUM, NTUM
      USE TRCOM1, ONLY : KDIRX
      IMPLICIT NONE
      CHARACTER(LEN=10),           INTENT(IN)   :: KFID
      CHARACTER(LEN=80),           INTENT(INOUT):: KUFDEV,KUFDCG
      REAL(8),                     INTENT(IN)   :: DR, DT, AMP, RHOA
      REAL(8),                     INTENT(INOUT):: TLMAX
      REAL(8),DIMENSION(NTUM),     INTENT(OUT)  :: PV,PVA,TL
      REAL(8),DIMENSION(NTUM,NRMP),INTENT(OUT)  :: FOUT
      INTEGER(4),                  INTENT(IN)   :: NTXMAX, NRAMAX, NRMAX, NSW, MDLXP
      INTEGER(4),                  INTENT(INOUT):: ICK
      INTEGER(4),                  INTENT(OUT)  :: NTLMAX, IERR
      INTEGER(4):: MDCHK, NRL, NRLMAX, NRX, NTX
      REAL(8)   :: RSL, F0, RGN
      REAL(8),DIMENSION(NRUM)     :: DERIV, RL, TMP0
      REAL(8),DIMENSION(4,NRUM)   :: U
      REAL(8),DIMENSION(NTUM,NRUM):: F2


      IF(MDLXP.EQ.0) THEN
         CALL UFREAD2_TIME(KDIRX,KUFDEV,KUFDCG,KFID,RL,TL,F2,NRLMAX,NTXMAX,NRUM,NTUM,MDCHK,IERR)
      ELSE
         CALL IPDB_MDS2(KUFDEV,KUFDCG,KFID,NRUM,NTUM,RL,TL,F2,NRLMAX,NTXMAX,IERR)
      ENDIF
!
!     All the variables which this subroutine should handle
!        have a derivative of zero on-axis.
!
      DERIV(1)=0.D0
!
!     Error check in case of a single radial point
!
      IF(IERR.NE.0.AND.NRLMAX.LE.1) THEN
         FOUT(1:NTUM,1:NRMP)=0.D0
         IF(NRLMAX.LE.1) IERR=1
         RETURN
      ENDIF
!
!     Time mesh normalization (from t=a to t=b -> t=0 to t=b-a)
!
      TL(2:NTXMAX)=(TL(2:NTXMAX)-TL(1))
      TL(1)=0.D0
      IF(ICK.NE.0) THEN
         IF(ICK.EQ.2.AND.TLMAX.EQ.0.D0) GOTO 1000
         IF(TLMAX.NE.TL(NTXMAX)) THEN
            WRITE(6,*) 'XX UF2DTP:',KFID,'UFILE HAS AN ERROR!'
            WRITE(6,*) TLMAX,TL(NTXMAX)
!            STOP
         ENDIF
      ENDIF
 1000 CONTINUE
      TLMAX=TL(NTXMAX)
      NTLMAX=INT(DINT(TL(NTXMAX)*1.D2)*1.D-2/DT)
      IF(ICK.NE.2) ICK=1
!
!     Calculate values suitable for arbitrary radial mesh using spline
!
      DO NTX=1,NTXMAX
         TMP0(1:NRLMAX)=F2(NTX,1:NRLMAX)
         CALL SPL1D(RL,TMP0,DERIV,U,NRLMAX,1,IERR)
         DO NRL=1,NRMAX
            IF(NSW.EQ.0) THEN
               RSL=(DBLE(NRL)-0.5D0)*DR
            ELSEIF(NSW.EQ.1) THEN
               RSL= DBLE(NRL)       *DR
            ENDIF
            CALL SPL1DF(RSL,F0,RL,U,NRLMAX,IERR)
            IF(IERR.NE.0) WRITE(6,600) "XX UF2DTP: SPL1DF ",KFID,": IERR=",IERR
            FOUT(NTX,NRL)=F0*AMP
         ENDDO
!
!     Edge values
!
         RGN=DBLE(NRMAX)*DR
         CALL SPL1DF(RGN,F0,RL,U,NRLMAX,IERR)
         IF(IERR.NE.0) WRITE(6,600) "XX UF2DTP: SPL1DF ",KFID,": IERR=",IERR
         PV(NTX)=F0*AMP
         IF(RHOA.NE.1.D0) THEN
            RGN=DBLE(NRAMAX)*DR
            CALL SPL1DF(RGN,F0,RL,U,NRLMAX,IERR)
            IF(IERR.NE.0) WRITE(6,600) "XX UF2DTP: SPL1DF ",KFID,": IERR=",IERR
            PVA(NTX)=F0*AMP
         ENDIF
      ENDDO

 600  FORMAT(' ',A18,A10,A7,I2)
      RETURN
      END SUBROUTINE UF2DTP

!     ***********************************************************

!           PRETREATMENT SUBROUTINE FOR UFILE INTERFACE

!     ***********************************************************

!     This subroutine is only used if MDLUF=2 and any subroutines
!     do not call this except UF2DS and UF2DSP.
!     In this subroutine, one can choose arbitrary slice time if one
!     did not choose it ahead of time. If one choose the time,
!     this checks its value consistent with the range of time.
!     One of the main function in this section is to make "Spline Array"
!     in order to interpolate various profiles radially.

!     IERR: negative value means "no data file".

      SUBROUTINE PRETREAT0(KFID,RL,TL,F2,U,NRFMAX,NTXMAX,ID,IERR)

      USE TRCOMM, ONLY : MDLJQ, NRUM, NTUM, TIME_INT
      IMPLICIT NONE
      CHARACTER(LEN=10)           ,INTENT(IN):: KFID
      REAL(8),DIMENSION(NRUM)     ,INTENT(IN) :: RL
      REAL(8),DIMENSION(NTUM)     ,INTENT(IN) :: TL
      REAL(8),DIMENSION(NTUM,NRUM),INTENT(IN) :: F2
      REAL(8),DIMENSION(4,NRUM)   ,INTENT(OUT):: U
      INTEGER(4),                  INTENT(IN) :: NRFMAX, NTXMAX, ID
      INTEGER(4),                  INTENT(INOUT):: IERR
      INTEGER(4):: NTSL, NTX, NTX_MIN
      REAL(8)   :: TL_MIN, TL_MIN_OLD
      REAL(8),DIMENSION(NRUM):: DERIV, TMP

      IF(IERR.EQ.1) THEN
         U(1:4,1:NRUM)=0.D0
         IF(KFID.EQ.'CURTOT'.AND.IERR.NE.0.AND.MDLJQ.EQ.0) MDLJQ=1
         IERR=-1
         RETURN
      ENDIF

      DERIV(1)=0.D0
      DERIV(NRFMAX)=0.D0

      NTSL=1
      IF(NTXMAX.NE.1) THEN
         IF(TIME_INT.LE.0.D0) THEN
 100        WRITE(6,500) 'INPUT ARBITRARY TIME:',TL(1),' -',TL(NTXMAX)
            READ(5,*,ERR=100) TIME_INT
            IF(TIME_INT.LT.TL(1).OR.TIME_INT.GT.TL(NTXMAX)) GOTO 100
            DO NTX=1,NTXMAX
               IF(ABS(TL(NTX)-TIME_INT).LE.1.D-5) THEN
                  NTSL=NTX
                  GOTO 1000
               ENDIF
            ENDDO

            TL_MIN=TL(NTXMAX)
            DO NTX=1,NTXMAX
               TL_MIN_OLD=TL_MIN
               TL_MIN=MIN(ABS(TL(NTX)-TIME_INT),TL_MIN)
               IF(TL_MIN_OLD.EQ.TL_MIN) THEN
                  NTX_MIN=NTX-1
                  GOTO 200
               ENDIF
            ENDDO
         ELSE
            IF(TIME_INT.GE.TL(1).AND.TIME_INT.LE.TL(NTXMAX)) THEN
               DO NTX=1,NTXMAX
                  IF(ABS(TL(NTX)-TIME_INT).LE.1.D-5) THEN
                     NTSL=NTX
                     GOTO 1000
                  ENDIF
               ENDDO

               TL_MIN=TL(NTXMAX)
               DO NTX=1,NTXMAX
                  TL_MIN_OLD=TL_MIN
                  TL_MIN=MIN(ABS(TL(NTX)-TIME_INT),TL_MIN)
                  IF(TL_MIN_OLD.EQ.TL_MIN) THEN
                     NTX_MIN=NTX-1
                     GOTO 200
                  ENDIF
               ENDDO
            ENDIF
            WRITE(6,*) '## DESIGNATED TIME_INT IS NOT IN THE RANGE.'
            WRITE(6,*) 'TIME_INT=',SNGL(TIME_INT),'RANGE=',SNGL(TL(1)),'-',SNGL(TL(NTXMAX))
         ENDIF

 200     WRITE(6,500) 'TIME_INT=',TIME_INT,' HAS BEEN REPLACED BY',TL(NTX_MIN)
         TIME_INT=TL(NTX_MIN)
         NTSL=NTX_MIN
 1000    CONTINUE
      ENDIF

      TMP(1:NRFMAX)=F2(NTSL,1:NRFMAX)
      CALL SPL1D(RL,TMP,DERIV,U,NRFMAX,ID,IERR)
      IF(IERR.NE.0) WRITE(6,*) 'XX PRETREAT0: SPL1D',KFID,': IERR=',IERR

      RETURN
 500  FORMAT(' ',A,F9.5,A,F9.5)
      END SUBROUTINE PRETREAT0

!     ***********************************************************

!           READING DATA FROM UFILES WITH NT INCREMENT

!     ***********************************************************

      SUBROUTINE TR_UFREAD

      USE TRCOMM, ONLY : NRM, NTUM, DT, NT, RKAP, RKAPU, RR, RRU, RA, RAU, BB, BBU, PHIA, PHIAU, PNBIU, VOLAU, RMJRHOU, NRMAX, &
     &                   RHOA, NROMAX, MDLUF, MDLEQN, RNU, RN, MDNI, MDLEOI, RTU, RT, QPU, QP, KUFDEV, PEX, PNBU, PICU, PECU,  &
     &                   PRF, WROTU, TTRHOU, DVRHOU, ABRHOU, ARRHOU, AR1RHOU, AR2RHOU, RMNRHOU, RKPRHOU, WROT, VTOR, TTRHO,    &
     &                   DVRHO, ABRHO, ARRHO, AR1RHO, AR2RHO, RMJRHO, RMNRHO, RKPRHO, MDPHIA, PI, RJCB, RHOM, RM, RHOG, RG,    &
     &                   PBMU, RNFU, PBM, RNF, MDLEQB, MDLJQ, AJU, AJNBU, AJ, AJNB, RMU0, DVRHOG, ABRHOG, TTRHOG, RDP, DR, BP, &
     &                   AR1RHOG, ARRHOG, AJTOR, NRAMAX, RIPA, PNSU, PTSU, PNSUA, PTSUA, PNS, PTS, PNSA, PTSA, ALP, PROFT1,    &
     &                   PROFT2, ANC, PNC, ANFE, PNFE, NSM, PZ, PZFE, PZC, PNSS, PNSSA
      USE TRCOM1, ONLY : INS, NTAMAX, NTXMAX, NTXMAX1, PNBI, TMU, TMU1
      IMPLICIT NONE
      INTEGER(4):: IERR, NR, NS, NTL, NTLL
      REAL(8)   :: ABRL, AJL, AJNBL, ANEAVE, ANI, ANZ, AR1RL, AR2RL, ARRL, DILUTE, DVRL, FACTOR0, FACTORM,    &
     &             FACTORP, PBML, PECL, PICDL, PICEL, PNBDL, PNBEL, PNSAL, PNSL, PROF, PTSAL, PTSL, QPL, RHO_A, RKAPL, RMJRL, &
     &             RMJRXL, RMNRL, RNDL, RNEL, RNFL, RNIL, RTDL, RTDM, RTEL, RTIL, TSL, TTRL, VOL, VOLM, WROTL

      REAL(8),DIMENSION(NRMAX):: AJTMP, DSRHO


      TSL=DT*DBLE(NT)
      CALL TIMESPL(TSL,RKAP,TMU1,RKAPU,NTXMAX1,NTUM,IERR)
      CALL TIMESPL(TSL,RR  ,TMU1,RRU  ,NTXMAX1,NTUM,IERR)
      CALL TIMESPL(TSL,RA  ,TMU1,RAU  ,NTXMAX1,NTUM,IERR)
      CALL TIMESPL(TSL,BB  ,TMU1,BBU  ,NTXMAX1,NTUM,IERR)
      CALL TIMESPL(TSL,PHIA,TMU1,PHIAU,NTXMAX1,NTUM,IERR)
      CALL TIMESPL(TSL,PNBI,TMU1,PNBIU,NTXMAX1,NTUM,IERR)

      CALL TIMESPL(TSL,VOLM,TMU,VOLAU,NTXMAX,NTUM,IERR)
      CALL TIMESPL(TSL,RMJRXL,TMU,RMJRHOU(1,NRMAX),NTXMAX,NTUM,IERR)

      IF(RHOA.NE.1.D0) NRMAX=NROMAX
      select case(MDLUF)
      case(1)
      DO NR=1,NRMAX
         IF(MDLEQN.EQ.0) THEN
            CALL TIMESPL(TSL,RNEL ,TMU,RNU(1,NR,1),NTXMAX,NTUM,IERR)
            CALL TIMESPL(TSL,RNDL ,TMU,RNU(1,NR,2),NTXMAX,NTUM,IERR)
            RN(NR,1)=RNEL
            RN(NR,2)=RNDL
            IF(MDNI.NE.0) THEN
               CALL TIMESPL(TSL,RNIL ,TMU,RNU(1,NR,3),NTXMAX,NTUM,IERR)
               RN(NR,3)=RNIL
            ENDIF
         ENDIF
         IF(INS.NE.0) THEN
            IF(MDLEOI.EQ.1) THEN
               CALL TIMESPL(TSL,RTDL ,TMU,RTU(1,NR,2),NTXMAX,NTUM,IERR)
               RT(NR,2)=RTDL
            ELSEIF(MDLEOI.EQ.2) THEN
               CALL TIMESPL(TSL,RTEL ,TMU,RTU(1,NR,1),NTXMAX,NTUM,IERR)
               RT(NR,1)=RTEL
            ENDIF
            CALL TIMESPL(TSL,RTIL ,TMU,RTU(1,NR,3),NTXMAX,NTUM,IERR)
            IF(INS.EQ.2) RT(NR,3)=RTIL
         ENDIF
         CALL TIMESPL(TSL,QPL ,TMU,QPU(1,NR),NTXMAX,NTUM,IERR)
         QP(NR)=QPL

         IF(KUFDEV.EQ.'X') THEN
            IF(PNBI.LT.12.D6) THEN
               PEX(NR,1)=PNBU(1,NR,1)
               PEX(NR,2)=PNBU(1,NR,2)
            ELSE
               PEX(NR,1)=PNBU(2,NR,1)
               PEX(NR,2)=PNBU(2,NR,2)
               IF(NT.EQ.NTAMAX) THEN
                  PEX(NR,1)=PNBU(3,NR,1)
                  PEX(NR,2)=PNBU(3,NR,2)
               ENDIF
            ENDIF
         ELSE
            CALL TIMESPL(TSL,PNBEL,TMU,PNBU(1,NR,1),NTXMAX,NTUM,IERR)
            CALL TIMESPL(TSL,PNBDL,TMU,PNBU(1,NR,2),NTXMAX,NTUM,IERR)
            IF(PNBEL.LT.0.D0) PNBEL=0.D0
            IF(PNBDL.LT.0.D0) PNBDL=0.D0
            PEX(NR,1)=PNBEL
            PEX(NR,2)=PNBDL
         ENDIF

         CALL TIMESPL(TSL,PICEL,TMU,PICU(1,NR,1),NTXMAX,NTUM,IERR)
         IF(PICEL.LT.0.D0) PICEL=0.D0
         CALL TIMESPL(TSL,PICDL,TMU,PICU(1,NR,2),NTXMAX,NTUM,IERR)
         IF(PICDL.LT.0.D0) PICDL=0.D0
         CALL TIMESPL(TSL,PECL ,TMU,PECU(1,NR  ),NTXMAX,NTUM,IERR)
         IF(PECL.LT.0.D0) PECL=0.D0
         PRF(NR,1)=PICEL+PECL
         PRF(NR,2)=PICDL
         CALL TIMESPL(TSL,WROTL,TMU,WROTU  (1,NR),NTXMAX,NTUM,IERR)
         CALL TIMESPL(TSL,TTRL ,TMU,TTRHOU (1,NR),NTXMAX,NTUM,IERR)
         CALL TIMESPL(TSL,DVRL ,TMU,DVRHOU (1,NR),NTXMAX,NTUM,IERR)
         CALL TIMESPL(TSL,ABRL ,TMU,ABRHOU (1,NR),NTXMAX,NTUM,IERR)
         CALL TIMESPL(TSL,ARRL ,TMU,ARRHOU (1,NR),NTXMAX,NTUM,IERR)
         CALL TIMESPL(TSL,AR1RL,TMU,AR1RHOU(1,NR),NTXMAX,NTUM,IERR)
         CALL TIMESPL(TSL,AR2RL,TMU,AR2RHOU(1,NR),NTXMAX,NTUM,IERR)
         CALL TIMESPL(TSL,RMJRL,TMU,RMJRHOU(1,NR),NTXMAX,NTUM,IERR)
         CALL TIMESPL(TSL,RMNRL,TMU,RMNRHOU(1,NR),NTXMAX,NTUM,IERR)
         CALL TIMESPL(TSL,RKAPL,TMU,RKPRHOU(1,NR),NTXMAX,NTUM,IERR)
         WROT(NR)=WROTL
         VTOR(NR)=WROTL*RMJRL
         TTRHO(NR)=TTRL
         DVRHO(NR)=DVRL
         ABRHO(NR)=ABRL
         ARRHO(NR)=ARRL
         AR1RHO(NR)=AR1RL
         AR2RHO(NR)=AR2RL
         RMJRHO(NR)=RMJRL
         RMNRHO(NR)=RMNRL
         RKPRHO(NR)=RKAPL
         IF(MDPHIA.EQ.0) THEN
            RHO_A=SQRT(PHIA/(PI*BB))
            RJCB(NR)=1.D0/RHO_A
            RHOM(NR)=RM(NR)*RHO_A
            RHOG(NR)=RG(NR)*RHO_A
         ELSE
            RHO_A=SQRT(VOLM/(2.D0*PI**2*RMJRXL))
            RJCB(NR)=1.D0/RHO_A
            RHOM(NR)=RM(NR)/RJCB(NR)
            RHOG(NR)=RG(NR)/RJCB(NR)
         ENDIF
         CALL TIMESPL(TSL,PBML,TMU,PBMU(1,NR),NTXMAX,NTUM,IERR)
         CALL TIMESPL(TSL,RNFL,TMU,RNFU(1,NR),NTXMAX,NTUM,IERR)
         PBM(NR)=PBML
         RNF(NR,1)=RNFL
      ENDDO
      CALL TRGFRG
      select case(MDLEQB)
      case(0)
         IF(MDLJQ.EQ.0) THEN ! *** MDLJQ ***
            NR=1
               CALL TIMESPL(TSL,AJL  ,TMU,AJU  (1,NR),NTXMAX,NTUM,IERR)
               CALL TIMESPL(TSL,AJNBL,TMU,AJNBU(1,NR),NTXMAX,NTUM,IERR)
               AJ(NR)=AJL
               AJNB(NR)=AJNBL
               FACTOR0=RMU0*BB*DVRHO(NR)*AJ(NR)/TTRHO(NR)**2
               FACTORP=DVRHOG(NR  )*ABRHOG(NR  )/TTRHOG(NR  )
               RDP(NR)=FACTOR0*DR/FACTORP
               BP(NR) =AR1RHOG(NR)*RDP(NR)/RR
               QP(NR)=TTRHOG(NR)*ARRHOG(NR)*DVRHOG(NR)/(4.D0*PI**2*RDP(NR))
            DO NR=2,NRMAX
               CALL TIMESPL(TSL,AJL  ,TMU,AJU  (1,NR),NTXMAX,NTUM,IERR)
               CALL TIMESPL(TSL,AJNBL,TMU,AJNBU(1,NR),NTXMAX,NTUM,IERR)
               AJ(NR)=AJL
               AJNB(NR)=AJNBL
               FACTOR0=RMU0*BB*DVRHO(NR)*AJ(NR)/TTRHO(NR)**2
               FACTORM=DVRHOG(NR-1)*ABRHOG(NR-1)/TTRHOG(NR-1)
               FACTORP=DVRHOG(NR  )*ABRHOG(NR  )/TTRHOG(NR  )
               RDP(NR)=(FACTOR0*DR+FACTORM*RDP(NR-1))/FACTORP
               BP(NR) =AR1RHOG(NR)*RDP(NR)/RR
               QP(NR)=TTRHOG(NR)*ARRHOG(NR)*DVRHOG(NR)/(4.D0*PI**2*RDP(NR))
            ENDDO
            NR=1
               FACTOR0=RR/(RMU0*DVRHO(NR))
               FACTORP=DVRHOG(NR  )*ABRHOG(NR  )
               AJTOR(NR)=FACTOR0*FACTORP*RDP(NR)/DR
            DO NR=2,NRMAX
               FACTOR0=RR/(RMU0*DVRHO(NR))
               FACTORM=DVRHOG(NR-1)*ABRHOG(NR-1)
               FACTORP=DVRHOG(NR  )*ABRHOG(NR  )
               AJTOR(NR)=FACTOR0*(FACTORP*RDP(NR)-FACTORM*RDP(NR-1))/DR
            ENDDO
         ELSE ! *** MDLJQ ***
            DO NR=1,NRMAX
               RDP(NR)=TTRHOG(NR)*ARRHOG(NR)*DVRHOG(NR)/(4.D0*PI**2*QP(NR))
               BP(NR)=AR1RHOG(NR)*RDP(NR)/RR
            ENDDO
         ENDIF ! *** MDLJQ ***
      case default
!        boundary condition for polidal flux at rhoa defined by exp. data
         VOL=0.D0
         DO NR=1,NRAMAX
            CALL TIMESPL(TSL,AJL ,TMU,AJU  (1,NR),NTXMAX,NTUM,IERR)
            VOL=VOL+DVRHO(NR)*DR
!!            DSRHO(NR)=DVRHO(NR)/(2.D0*PI*RMJRHO(NR))
            DSRHO(NR)=DVRHO(NR)/(2.D0*PI*RR)
            AJTMP(NR)=AJL
         ENDDO
         RIPA=SUM(AJTMP(1:NRMAX)*DSRHO(1:NRMAX))*DR/1.D6

         DO NR=1,NRMAX
            CALL TIMESPL(TSL,AJNBL,TMU,AJNBU(1,NR),NTXMAX,NTUM,IERR)
            AJNB(NR)=AJNBL
            BP(NR)=AR1RHOG(NR)*RDP(NR)/RR
            QP(NR)=TTRHOG(NR)*ARRHOG(NR)*DVRHOG(NR)/(4.D0*PI**2*RDP(NR))
         ENDDO
      end select
      case(3)
      DO NR=1,NRMAX
         CALL TIMESPL(TSL,RNEL ,TMU,RNU(1,NR,1),NTXMAX,NTUM,IERR)
         CALL TIMESPL(TSL,RNDL ,TMU,RNU(1,NR,2),NTXMAX,NTUM,IERR)
         CALL TIMESPL(TSL,RNIL ,TMU,RNU(1,NR,3),NTXMAX,NTUM,IERR)
         RN(NR,1)=RNEL
         RN(NR,2)=RNDL
         RN(NR,3)=RNIL
!         CALL TIMESPL(TSL,QPL ,TMU,QPU(1,NR),NTXMAX,NTUM,IERR)
!         QP(NR)=QPL
         CALL TIMESPL(TSL,PNBEL,TMU,PNBU(1,NR,1),NTXMAX,NTUM,IERR)
         CALL TIMESPL(TSL,PNBDL,TMU,PNBU(1,NR,2),NTXMAX,NTUM,IERR)
!         IF(PNBEL.LT.0.D0) PNBEL=0.D0
!         IF(PNBDL.LT.0.D0) PNBDL=0.D0
         PEX(NR,1)=PNBEL
         PEX(NR,2)=PNBDL
         CALL TIMESPL(TSL,PICEL,TMU,PICU(1,NR,1),NTXMAX,NTUM,IERR)
!         IF(PICEL.LT.0.D0) PICEL=0.D0
         CALL TIMESPL(TSL,PICDL,TMU,PICU(1,NR,2),NTXMAX,NTUM,IERR)
!         IF(PICDL.LT.0.D0) PICDL=0.D0
         CALL TIMESPL(TSL,PECL ,TMU,PECU(1,NR  ),NTXMAX,NTUM,IERR)
!         IF(PECL.LT.0.D0) PECL=0.D0
         IF(TSL.LE.1.D0) THEN
            PRF(NR,1)=0.D0
            PRF(NR,2)=0.D0
         ELSE
            DO NTL=1,NTXMAX
               IF(TMU(NTL).GT.TSL) THEN
                  NTLL=NTL
                  GOTO 1000
               ENDIF
            ENDDO
            NTLL=NTXMAX
 1000       PRF(NR,1)=PICU(NTLL,NR,1)+PECU(NTLL,NR)
            PRF(NR,2)=PICU(NTLL,NR,2)
         ENDIF
         CALL TIMESPL(TSL,TTRL ,TMU,TTRHOU (1,NR),NTXMAX,NTUM,IERR)
         CALL TIMESPL(TSL,DVRL ,TMU,DVRHOU (1,NR),NTXMAX,NTUM,IERR)
         CALL TIMESPL(TSL,ABRL ,TMU,ABRHOU (1,NR),NTXMAX,NTUM,IERR)
         CALL TIMESPL(TSL,ARRL ,TMU,ARRHOU (1,NR),NTXMAX,NTUM,IERR)
         CALL TIMESPL(TSL,AR1RL,TMU,AR1RHOU(1,NR),NTXMAX,NTUM,IERR)
         CALL TIMESPL(TSL,AR2RL,TMU,AR2RHOU(1,NR),NTXMAX,NTUM,IERR)
         CALL TIMESPL(TSL,RMJRL,TMU,RMJRHOU(1,NR),NTXMAX,NTUM,IERR)
         CALL TIMESPL(TSL,RMNRL,TMU,RMNRHOU(1,NR),NTXMAX,NTUM,IERR)
         CALL TIMESPL(TSL,RKAPL,TMU,RKPRHOU(1,NR),NTXMAX,NTUM,IERR)
         TTRHO(NR)=TTRL
         DVRHO(NR)=DVRL
         ABRHO(NR)=ABRL
         ARRHO(NR)=ARRL
         AR1RHO(NR)=AR1RL
         AR2RHO(NR)=AR2RL
         RMJRHO(NR)=RMJRL
         RMNRHO(NR)=RMNRL
         RKPRHO(NR)=RKAPL
         IF(MDPHIA.EQ.0) THEN
            RHO_A=SQRT(PHIA/(PI*BB))
            RJCB(NR)=1.D0/RHO_A
            RHOM(NR)=RM(NR)*RHO_A
            RHOG(NR)=RG(NR)*RHO_A
         ELSE
            RHO_A=SQRT(VOLM/(2.D0*PI**2*RMJRXL))
            RJCB(NR)=1.D0/RHO_A
            RHOM(NR)=RM(NR)/RJCB(NR)
            RHOG(NR)=RG(NR)/RJCB(NR)
         ENDIF
      ENDDO
      end select
      CALL TRGFRG

      IF(MDLUF.EQ.1) THEN
         DO NS=1,2
            CALL TIMESPL(TSL,PNSL ,TMU,PNSU (1,NS),NTXMAX,NTUM,IERR)
            CALL TIMESPL(TSL,PTSL ,TMU,PTSU (1,NS),NTXMAX,NTUM,IERR)
            CALL TIMESPL(TSL,PNSAL,TMU,PNSUA(1,NS),NTXMAX,NTUM,IERR)
            CALL TIMESPL(TSL,PTSAL,TMU,PTSUA(1,NS),NTXMAX,NTUM,IERR)
            PNS (NS)=PNSL
            PTS (NS)=PTSL
            PNSA(NS)=PNSAL
            PTSA(NS)=PTSAL
         ENDDO
         IF(MDNI.NE.0) THEN
            NS=3
            CALL TIMESPL(TSL,PNSL ,TMU,PNSU (1,NS),NTXMAX,NTUM,IERR)
            CALL TIMESPL(TSL,PTSL ,TMU,PTSU (1,NS),NTXMAX,NTUM,IERR)
            CALL TIMESPL(TSL,PNSAL,TMU,PNSUA(1,NS),NTXMAX,NTUM,IERR)
            CALL TIMESPL(TSL,PTSAL,TMU,PTSUA(1,NS),NTXMAX,NTUM,IERR)
            PNS (NS)=PNSL
            PTS (NS)=PTSL
            PNSA(NS)=PNSAL
            PTSA(NS)=PTSAL
         ENDIF
         IF(RHOA.NE.1.D0) THEN
            CALL TIMESPL(TSL,RTDM,TMU,RTU(1,NRMAX,2),NTXMAX,NTUM,IERR)
            DO NR=NRAMAX+1,NRMAX
               PROF    =(1.D0-(ALP(1)*RM(NR))**PROFT1)**PROFT2
               CALL TIMESPL(TSL,RTEL,TMU,RTU(1,NR,1),NTXMAX,NTUM,IERR)
               CALL TIMESPL(TSL,RTDL,TMU,RTU(1,NR,2),NTXMAX,NTUM,IERR)
               RT(NR,1)= RTEL
               RT(NR,2)= RTDL
               RT(NR,3)= RTDL
               RT(NR,4)=(RTDL-RTDM)*PROF+RTDM
            ENDDO
            IF(MDNI.EQ.0) THEN
               DO NR=NRAMAX+1,NRMAX
                  CALL TIMESPL(TSL,RTDL,TMU,RTU(1,NR,2),NTXMAX,NTUM,IERR)
                  PROF    =(1.D0-(ALP(1)*RM(NR))**PROFT1)**PROFT2
                  RT(NR,3)=(RTDL-RTDM)*PROF+RTDM
               ENDDO
            ENDIF
         ENDIF
      ENDIF

!     *** CALCULATE PZC,PZFE ***

      CALL TRZEFF

!     *** CALCULATE ANEAVE ***

      ANEAVE=SUM(RN(1:NRMAX,1)*RM(1:NRMAX))*2.D0*DR

!     *** CALCULATE IMPURITY DENSITY
!                ACCORDING TO ITER PHYSICS DESIGN GUIDELINE ***

      IF(MDLUF.NE.3) THEN
         DO NR=1,NRMAX
            ANC (NR)= (0.9D0+0.60D0*(0.7D0/ANEAVE)**2.6D0)*PNC*1.D-2*RN(NR,1)
            ANFE(NR)= (0.0D0+0.05D0*(0.7D0/ANEAVE)**2.3D0)*PNFE*1.D-2*RN(NR,1)
            ANI = SUM(PZ(2:NSM)*RN(NR,2:NSM))
            ANZ = PZFE(NR)*ANFE(NR)+PZC(NR)*ANC(NR)
            DILUTE = 1.D0-ANZ/ANI
            RN(NR,2:NSM) = RN(NR,2:NSM)*DILUTE
         ENDDO
         PNSS(1)=PNS(1)
         PNSS(2:NSM)=PNS(2:NSM)*DILUTE
         PNSS(7)=PNS(7)
         PNSS(8)=PNS(8)
         IF(RHOA.NE.1.D0) THEN
            PNSSA(1)=PNSA(1)
            DO NS=2,NSM
               PNSSA(NS)=PNSA(NS)*DILUTE
            ENDDO
            PNSSA(7)=PNSA(7)
            PNSSA(8)=PNSA(8)
         ENDIF
      ELSE
!$$$         ANC(1:NRMAX)=1.D0/3.D1*RN(1:NRMAX,1)
         PNSS(1:NSM)=PNS(1:NSM)
         PNSS(7)=PNS(7)
         PNSS(8)=PNS(8)
         IF(RHOA.NE.1.D0) THEN
            PNSSA(1:NSM)=PNSA(1:NSM)*DILUTE
            PNSSA(7)=PNSA(7)
            PNSSA(8)=PNSA(8)
         ENDIF
      ENDIF

      IF(RHOA.NE.1.D0) NRMAX=NRAMAX

      RETURN
      END SUBROUTINE TR_UFREAD

!     ******

      SUBROUTINE TR_UFREAD_S

      USE TRCOMM, ONLY : ABRHO, ABRHOU, ANC, ANFE, AR1RHO, AR1RHOG, AR1RHOU, AR2RHO, AR2RHOU, ARRHO, ARRHOG, ARRHOU, BB, BBU, &
     &                   BP, DR, DVRHO, DVRHOG, DVRHOU, MDLEOI, MDLEQN, MDNI, MDPHIA, NRAMAX, NRMAX, NROMAX, NSM, NTS, PECU,  &
     &                   PEX, PHIA, PHIAU, PI, PICU, PNBU, PNC, PNFE, PNS, PNSA, PNSS, PNSSA, PRF, PZ, PZC, PZFE, QP, RA, RAU,&
     &                   RDP, RG, RHOA, RHOG, RHOM, RJCB, RKAP, RKAPU, RKPRHO, RKPRHOU, RM, RMJRHO, RMJRHOU, RMNRHO, RMNRHOU, &
     &                   RN, RNF, RNFU, RNU, RR, RRU, RT, RTU, TTRHO, TTRHOG, TTRHOU, VOLAU, VTOR, WROT, WROTU
      USE TRCOM1, ONLY : INS
      IMPLICIT NONE
      INTEGER(4):: NR, NS
      REAL(8)   :: ANEAVE, ANI, ANZ, DILUTE, RHO_A, RMJRXL, VOLM


      RKAP=RKAPU(NTS)
      RR=RRU(NTS)
      RA=RAU(NTS)
      BB=BBU(NTS)
      PHIA=PHIAU(NTS)

      IF(RHOA.NE.1.D0) NRMAX=NROMAX
      VOLM=VOLAU(1)
      RMJRXL=RMJRHOU(1,NRMAX)
      DO NR=1,NRMAX
         IF(MDLEQN.EQ.0) THEN
            RN(NR,1)=RNU(1,NR,1)
            RN(NR,2)=RNU(1,NR,2)
            IF(MDNI.NE.0) RN(NR,3)=RNU(1,NR,3)
         ENDIF
         IF(INS.NE.0) THEN
            IF(MDLEOI.EQ.1) THEN
               RT(NR,2)=RTU(1,NR,2)
            ELSEIF(MDLEOI.EQ.2) THEN
               RT(NR,1)=RTU(1,NR,1)
            ENDIF
            IF(INS.EQ.2) RT(NR,3)=RTU(1,NR,3)
         ENDIF
         PEX(NR,1)=PNBU(1,NR,1)
         PEX(NR,2)=PNBU(1,NR,2)
         PRF(NR,1)=PICU(1,NR,1)+PECU(1,NR)
         PRF(NR,2)=PICU(1,NR,2)
         WROT(NR) =WROTU(1,NR)
         VTOR(NR) =WROTU(1,NR)*RMJRHOU(1,NR)
         TTRHO(NR)=TTRHOU(1,NR)
         DVRHO(NR)=DVRHOU(1,NR)
         ABRHO(NR)=ABRHOU(1,NR)
         ARRHO(NR)=ARRHOU(1,NR)
         AR1RHO(NR)=AR1RHOU(1,NR)
         AR2RHO(NR)=AR2RHOU(1,NR)
         RMJRHO(NR)=RMJRHOU(1,NR)
         RMNRHO(NR)=RMNRHOU(1,NR)
         RKPRHO(NR)=RKPRHOU(1,NR)
         IF(MDPHIA.EQ.0) THEN
            RHO_A=SQRT(PHIA/(PI*BB))
            RJCB(NR)=1.D0/RHO_A
            RHOM(NR)=RM(NR)*RHO_A
            RHOG(NR)=RG(NR)*RHO_A
         ELSE
            RHO_A=SQRT(VOLM/(2.D0*PI**2*RMJRXL))
            RJCB(NR)=1.D0/RHO_A
            RHOM(NR)=RM(NR)/RJCB(NR)
            RHOG(NR)=RG(NR)/RJCB(NR)
         ENDIF
         RNF(NR,1)=RNFU(1,NR)
      ENDDO
      CALL TRGFRG
      DO NR=1,NRMAX
         RDP(NR)=TTRHOG(NR)*ARRHOG(NR)*DVRHOG(NR)/(4.D0*PI**2*QP(NR))
         BP(NR)=AR1RHOG(NR)*RDP(NR)/RR
      ENDDO

!     *** CALCULATE PZC,PZFE ***

      CALL TRZEFF

!     *** CALCULATE ANEAVE ***

      ANEAVE=SUM(RN(1:NRMAX,1)*RM(1:NRMAX))*2.D0*DR

!     *** CALCULATE IMPURITY DENSITY
!                ACCORDING TO ITER PHYSICS DESIGN GUIDELINE ***

      DO NR=1,NRMAX
         ANC (NR)= (0.9D0+0.60D0*(0.7D0/ANEAVE)**2.6D0)*PNC*1.D-2*RN(NR,1)
         ANFE(NR)= (0.0D0+0.05D0*(0.7D0/ANEAVE)**2.3D0)*PNFE*1.D-2*RN(NR,1)
         ANI = SUM(PZ(2:NSM)*RN(NR,2:NSM))
         ANZ = PZFE(NR)*ANFE(NR)+PZC(NR)*ANC(NR)
         DILUTE = 1.D0-ANZ/ANI
         RN(NR,2:NSM) = RN(NR,2:NSM)*DILUTE
      ENDDO
      PNSS(1)=PNS(1)
      PNSS(2:NSM)=PNS(2:NSM)*DILUTE
      PNSS(7)=PNS(7)
      PNSS(8)=PNS(8)
      IF(RHOA.NE.1.D0) THEN
         PNSSA(1)=PNSA(1)
         PNSSA(2:NSM)=PNSA(2:NSM)*DILUTE
         PNSSA(7)=PNSA(7)
         PNSSA(8)=PNSA(8)
      ENDIF

      IF(RHOA.NE.1.D0) NRMAX=NRAMAX

      RETURN
      END SUBROUTINE TR_UFREAD_S

!     ***********************************************************

!           CALCULATING FLUX FROM SOURCE TERM FILES

!     ***********************************************************

      SUBROUTINE FLUX

      USE TRCOMM, ONLY : DR, DVRHO, MDLFLX, NRM, NRMAX, NSM, PZ, RGFLX, SNBU, SWLU
      IMPLICIT NONE
      INTEGER(4):: NR
      REAL(8),DIMENSION(NRMAX)::  SALEL, SALIL


      IF(MDLFLX.EQ.0) THEN
         RGFLX(1:NRMAX,1:NSM)=0.D0
      ELSE
         DO NR=1,NRMAX
            SALEL(NR)=SNBU(1,NR,1)+SWLU(1,NR)/PZ(2)
            RGFLX(NR,1)=SUM(SALEL(1:NR)*DVRHO(1:NR))*DR
            SALIL(NR)=SNBU(1,NR,2)+SWLU(1,NR)
            RGFLX(NR,2)=SUM(SALIL(1:NR)*DVRHO(1:NR))*DR
            RGFLX(NR,3:NSM)=0.D0
         ENDDO
      ENDIF

      RETURN
      END SUBROUTINE FLUX
