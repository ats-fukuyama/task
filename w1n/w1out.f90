MODULE w1out

  PRIVATE
  PUBLIC W1PRNT,W1FILE

CONTAINS

!     ******* OUTPUT TO LINE PRINTER *******

  SUBROUTINE W1PRNT
    USE w1comm
    USE w1prep,ONLY: w1fftl
    IMPLICIT NONE
    INTEGER:: NX,NA,NXANT1,NXANT2,NZ,NS,I,IC
    REAL(rkind):: RNORM,PJK
    CHARACTER(LEN=8):: INDEX
    REAL(rkind),DIMENSION(:,:),ALLOCATABLE:: TEMP

    ALLOCATE(TEMP(NXTMAX,NZPMAX))

    RNORM=1.D0/PANT
    IF(NPRINT.LE.-1) THEN
       WRITE(6,611) (PROFPN(NX,4),NX=1,NXPMAX)
611    FORMAT(1H ,'*** ALPHA PARTICLE DENSITY PROFILE ***'/ &
             (1H ,1P10D12.4))
    ENDIF

    WRITE(6,620) PANT*RNORM,PANT1*RNORM,PANT2*RNORM
    WRITE(6,661) PWALL*RNORM,PWALL1*RNORM,PWALL2*RNORM, &
                 PABSTT*RNORM,AJCDT*RNORM,PERR*RNORM
    WRITE(6,664)
    WRITE(6,663) (NS,PABSXZ(NS)*RNORM, &
                  AHLT(NS,1)*RNORM,AHLT(NS,2)*RNORM, &
                 (AHLT(NS,1)+AHLT(NS,2))*RNORM, &
                  AHLT(NS,3)*RNORM,AHLT(NS,4)*RNORM, &
                 (AHLT(NS,3)+AHLT(NS,4))*RNORM,NS=1,NSMAX)
    WRITE(6,662)
    DO NA = 1 , NAMAX
       WRITE(6,621) NA,RANT1(NA),XANT1(NA),RANT2(NA),XANT2(NA)
    END DO

    DO NX=1,NXTMAX
       IF(NX.LE.NXVMAX) THEN
          NXPRNT(NX)=NXPMAX+NXVMAX+NX
       ELSE
          NXPRNT(NX)=NX-NXVMAX
       ENDIF
    END DO

    IF(NPRINT.LE.0) RETURN

    NXANT1=NXPMAX+NXVMAX+NXVMAX/2+1
    NXANT2=NXPMAX       +NXVMAX/2
    WRITE(6,633)
    WRITE(6,601)
    DO NZ=1,NZPMAX
       WRITE(6,634) ZA(NZ),CJ1(NZ),CE2DA(NZ,NXANT1,2), &
                           CJ2(NZ),CE2DA(NZ,NXANT2,2)
    END DO

    CALL W1FFTL(CJ1,NZPMAX,0)
    CALL W1FFTL(CJ2,NZPMAX,0)
    WRITE(6,635) (NS,NS=1,NSMAX)
    WRITE(6,601)
    DO NZ=1,NZPMAX
       PJK=ABS(CJ1(NZ))**2+ABS(CJ2(NZ))**2
       WRITE(6,636) AKZ(NZ),PJK*RNORM,PANTK(NZ)*RNORM, &
                    (PAKT(NZ,I)*RNORM,I=1,3), &
                    (PAK(NZ,NS)*RNORM,NS=1,NSMAX)
    END DO

    CALL W1FFTL(CJ1,NZPMAX,1)
    CALL W1FFTL(CJ2,NZPMAX,1)

    IF(NPRINT.LE.1) RETURN

    WRITE(6,630) (NS,NS=1,NSMAX)
    WRITE(6,601)
    DO NX=1,NXPMAX
       WRITE(6,631) XAM(NX),FLUXX(NX), &
                    (PABSX(NX,NS),NS=1,NSMAX)
    END DO
    WRITE(6,632) PABSTT,(PABSXZ(NS),NS=1,NSMAX)
    WRITE(6,6321)       (PABSXZ(NS)/PABSTT,NS=1,NSMAX)

    IF(NPRINT.LE.2) RETURN

    DO IC=1,3
       DO NX=1,NXTMAX
          DO NZ=1,NZPMAX
             TEMP(NX,NZ)=ABS(CE2DA(NZ,NX,IC))
          END DO
       END DO

       IF(IC.EQ.1) INDEX='ABS(EX) '
       IF(IC.EQ.2) INDEX='ABS(EY) '
       IF(IC.EQ.3) INDEX='ABS(EZ) '
       CALL W1PRTS(TEMP,1,INDEX)

       IF(NPRINT.LE.3) RETURN

       DO  NX=1,NXTMAX
          DO NZ=1,NZPMAX
             TEMP(NX,NZ)=REAL(CE2DA(NZ,NX,IC))
          END DO
       END DO
       IF(IC.EQ.1) INDEX='REAL(EX)'
       IF(IC.EQ.2) INDEX='REAL(EY)'
       IF(IC.EQ.3) INDEX='REAL(EZ)'
       CALL W1PRTS(TEMP,1,INDEX)

       DO NX=1,NXTMAX
          DO NZ=1,NZPMAX
             TEMP(NX,NZ)=IMAG(CE2DA(NZ,NX,IC))
          END DO
       END DO
       IF(IC.EQ.1) INDEX='IMAG(EX)'
       IF(IC.EQ.2) INDEX='IMAG(EY)'
       IF(IC.EQ.3) INDEX='IMAG(EZ)'
       CALL W1PRTS(TEMP,1,INDEX)
    END DO
    DEALLOCATE(TEMP)
    RETURN

601 FORMAT(1H )
620 FORMAT(1H0,'*** POWER PARTITION *** (UNIT = MW/M)'/ &
           1H ,4X,'PANT   =',1PE12.4,8X, &
                  'PANT1  =',  E12.4,4X,'PANT2  =',E12.4)
661 FORMAT(1H ,4X,'PWALL  =',1PE12.4,8X, &
                  'PWALL1 =',  E12.4,4X,'PWALL2 =',E12.4/ &
           1H ,4X,'PABST  =',1PE12.4,8X,'AJCDT  =',E12.4, &
               4X,'PERR   =',1PE12.4)
663 FORMAT(1H ,I5,1P7E10.2)
662 FORMAT(1H ,'*** ANTENNA LOADING *** (UNIT = OHM/M)'/ &
           1H ,'IA  ',7X,'RANTH',10X,'XANTH',15X,'RANTL',10X,'XANTL')
664 FORMAT(1H ,'   NS   PABS       I R      I NR      I TOT', &
                            '      I RT     I NRT     I TOTT')
621 FORMAT(1H ,1X,I3,2X,2(3X,1PE12.4),5X,2(3X,E12.4))
630 FORMAT(1H0,5X,'X',10X,'FLUXX(X)',1X, &
               2X,5(6X,'P-NS',I1,'(X)':))
631 FORMAT(1H ,F10.5,1PE14.4,2X,5(E14.4))
632 FORMAT(1H0,3X,'TOTAL',18X,1PE14.4,2X,5(E14.4))
6321 FORMAT(1H ,42X,5(F14.5))
633 FORMAT(1H0,5X,'Z',20X,'CJ1',23X,'EY1',25X,'CJ2',23X,'EY2')
634 FORMAT(1H ,F10.5,2(4X,1P2E12.4,2X,2E12.4))
635 FORMAT(1H0,3X,'KZ',6X,'J**2(KZ)',4X,'P-ANT(KZ)',3X,'P-TOT(KZ)', &
               3X,'P-IBW(KZ)',3X,'P-ABS(KZ)', &
               2X,5(3X,'P-NS',I1,'(KZ)':))
636 FORMAT(1H ,F8.4,1PE12.4,E12.4,E12.4,E12.4,E12.4,2X,5(E12.4))
  END SUBROUTINE W1PRNT

!     ====== OUTPUT OF 2-D DATA TO LINE PRINTER ======

  SUBROUTINE W1PRTS(TEMP,IND,INDEX)
    USE w1comm
    IMPLICIT NONE
    INTEGER,INTENT(IN):: IND
    CHARACTER(LEN=8):: INDEX
    REAL(rkind),INTENT(IN):: TEMP(NXTMAX,NZPMAX)
    INTEGER:: NX,NZ,IEND,NXX,I

    DO NZ=1,NZPMAX,9
       IEND=NZ+8
       IF(IEND.GT.NZ) IEND=NZPMAX
       WRITE(6,601) INDEX
       WRITE(6,602) (ZA(I),I=NZ,IEND)
       WRITE(6,603)
       DO NX=1,NXPMAX
          NXX=NX
          IF(IND.EQ.1) NXX=NXPRNT(NX)
          WRITE(6,604) XAM(NXX),(TEMP(NXX,I),I=NZ,IEND)
       END DO
    END DO
    RETURN
601 FORMAT(1H0,A8)
602 FORMAT(1H ,'        Z=',9(F8.4,4X))
603 FORMAT(1H ,'   X')
604 FORMAT(1H ,F8.4,2X,9(1PE12.4))
  END SUBROUTINE W1PRTS

!     ******* OUTPUT TO FILE 21 *******

  SUBROUTINE W1FILE
    USE w1comm
    IMPLICIT NONE
    INTEGER:: NS,NX,NZ,IC,I
    REAL(rkind):: AZ
    CHARACTER(LEN=8)::  INDEX

    IF(NFILE.LE.0) RETURN

    AZ=0.D0
    WRITE(21) REAL(RF),REAL(BB),REAL(AJYH(1)),REAL(AJYL(1)), &
              REAL(RR),REAL(RA),REAL(RD),REAL(RB),REAL(RZ), &
              REAL(AZ),REAL(WALLR),REAL(RKZ)
    WRITE(21) NXPMAX,NXVMAX,NZPMAX,NSMAX
    WRITE(21) (REAL(PA(NS)),REAL(PZ(NS)),REAL(PN(NS)), & 
               REAL(PTPP(NS)),REAL(PTPR(NS)),REAL(PU(NS)), &
               REAL(PNS(NS)),REAL(PTS(NS)),REAL(PZCL(NS)), &
               NS=1,NSMAX)
    WRITE(21) (REAL(XAM(NX)),NXPRNT(NX),NX=1,NXTMAX)

    WRITE(21) PANT,PANT1,PANT2,PWALL,PWALL1,PWALL2,PIBW,PIBW1,PIBW2, &
              PABSTT,(PABSXZ(NS),NS=1,NSMAX),PERR, &
              RANT1,RANT2,XANT1,XANT2
    WRITE(21) (FLUXX(NX),NX=1,NXTMAX)
    WRITE(21) ((PABSX(NX,NS),NX=1,NXPMAX),NS=1,NSMAX)

    WRITE(21) (REAL(ZA(NZ)),NZ=1,NZPMAX)
    WRITE(21) (REAL(AKZ(NZ)),NZ=1,NZPMAX)
    WRITE(21) (PANTK(NZ),(PAKT(NZ,I),I=1,3), &
              (PAK(NZ,NS),NS=1,NSMAX),NZ=1,NZPMAX)

    IF(NFILE.LE.1) GOTO 9000

    DO IC=1,3
       IF(IC.EQ.1) INDEX='EFIELD-X'
       IF(IC.EQ.2) INDEX='EFIELD-Y'
       IF(IC.EQ.3) INDEX='EFIELD-Z'
       WRITE(21) INDEX
       WRITE(21) ((REAL(CE2DA(NZ,NX,IC)), &
                   IMAG(CE2DA(NZ,NX,IC)),NX=1,NXTMAX),NZ=1,NZPMAX)
    END DO

 9000  CONTINUE
    INDEX='END-DATA'
    WRITE(21) INDEX
    WRITE(6,680)
    RETURN
680 FORMAT(1H0,'***** DATA SAVED IN FILE 21 *****'/)
  END SUBROUTINE W1FILE
END MODULE w1out
