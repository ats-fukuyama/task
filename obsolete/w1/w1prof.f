C
C     ****** TASK/W1 (SUBROUTINE LIBRARY 1) ******
C
C     ****** SET PROFILES ******
C
      SUBROUTINE W1PROF
C
      IMPLICIT COMPLEX*16(C),REAL*8(A-B,D-H,O-Z)
C
      INCLUDE 'w1comm.f'
      PARAMETER (NZPM=2**NZLM)
      PARAMETER (NXTM=NXPM+2*NXVM,NXM=NXPM*6+10,NXQ=NXPM+1)
      COMMON /W1CNST/ AEE,AME,AMM,VC,EPS0,AMYU0,PI
      COMMON /W1PRM1/ NXP,NXV,NXT,ISMAX,IAMAX
      COMMON /W1PRM2/ RF,RKZ,BB,RR,RA,RD,RB,WALLR
      COMMON /W1PRM3/ PA(ISM),PZ(ISM),PN(ISM),PTPP(ISM),PTPR(ISM),
     &                PNS(ISM),PTS(ISM),PU(ISM),PZCL(ISM)
      COMMON /W1CTRL/ DRF,DRKZ,DXFACT,DXWDTH,APRFPN,APRFTR,APRFTP,
     &                NPRINT,NFILE,NGRAPH,NLOOP,NSYM,NFLR,NALPHA,
     &                NSYS,NDISP,NXABS
      COMMON /W1XDAT/ XA(NXQ),XAM(NXTM)
      COMMON /W1PRF1/ PROFB(NXPM),PROFPN(NXPM,ISM),PROFPU(NXPM,ISM),
     &                PROFTR(NXPM,ISM),PROFTP(NXPM,ISM)
      COMMON /W1QCTL/ XDMAX,DXD,NDMAX,NHARM,IHARM(ISM),MATL,NMODEL
      COMMON /W1PRM5/ EPSH
C
      IF(NSYS.EQ.0) THEN
         DO 100 NX =1,NXP
            PROFB(NX)=RR/(RR+XAM(NX))
  100    CONTINUE
      ELSE
         DO 110 NX=1,NXP
            PROFB(NX)=RR/(RR+XAM(NX))+EPSH*(XAM(NX)/RA)**2
  110    CONTINUE
      ENDIF
C
      DO 200 IS = 1 , ISMAX
      DO 200 NX = 1 , NXP
         X=XAM(NX)/RA
         PROFPN(NX,IS)=(PN  (IS)-PNS(IS))*(1.D0-X*X)**APRFPN + PNS(IS)
         PROFTR(NX,IS)=(PTPR(IS)-PTS(IS))*(1.D0-X*X)**APRFTR + PTS(IS)
C        PROFTR(NX,IS)= PTPR(IS)*EXP(-3.D0*X*X)
         PROFTP(NX,IS)=(PTPP(IS)-PTS(IS))*(1.D0-X*X)**APRFTP + PTS(IS)
C        PROFTP(NX,IS)= PTPP(IS)*EXP(-3.D0*X*X)
         PROFPU(NX,IS)=PU(IS)
 200  CONTINUE
C
      IF(NALPHA.LT.2.OR.ISM.LT.4) RETURN
C
      ISMAX=4
      PA(4)=4
      PZ(4)=2
      FTAUS  =0.75D0*PI*SQRT(PI)*(EPS0/AEE)**2*(4.D0*AMM/AEE)*(AME/AEE)
      VALPHA=SQRT(2.D0*3.5D6*AEE/(4.D0*AMM))
      DO 300 NX=1,NXP
         VTE   =SQRT(2.D0*PROFTR(NX,1)*1.D3*AEE/AME)
         VCRIT3=.75D0*SQRT(PI)*AME*(PROFPN(NX,2)/(AMM*PA(2))
     &                             +PROFPN(NX,3)/(AMM*PA(3)))
     &          /PROFPN(NX,1)
         VCRIT =VTE*VCRIT3**(1.D0/3.D0)
         ALNLAM=16.1-1.15*LOG10(PROFPN(NX,1))+2.3*LOG10(PROFTR(NX,1))
         TAUS  =FTAUS*VTE**3/(PROFPN(NX,1)*1.D20*ALNLAM)
         TI    =0.5D0*(PROFTR(NX,2)+PROFTR(NX,3))
         FH    = (TI/37.D0)+5.45D0/(3.D0+TI/(1.D0+(TI/37.5)**2.8))
         SALPHA=PROFPN(NX,2)*PROFPN(NX,3)*3.7D22*TI**(-2.D0/3.D0)
     &          *EXP(-20.D0*TI**(-1.D0/3.D0))/FH
         PROFPN(NX,4)=SALPHA*TAUS*LOG(1.D0+(VALPHA/VCRIT)**3)/3.D20
         T=3.5D3*(0.5D0*(1.D0-HBEAM(VALPHA/VCRIT)))
     &          /(LOG(1.D0+(VALPHA/VCRIT)**3)/3.D0)
         PROFTP(NX,4)=T
         PROFTR(NX,4)=T
  300 CONTINUE
      PN(4)=PROFPN(NXP/2,4)
      PNS(4)=1.D-6*PN(4)
      PTPR(4)=PROFTR(NXP/2,4)
      PTPP(4)=PROFTP(NXP/2,4)
      PTS(4) =PROFTP(NXP,4)
      WRITE(6,601) PN(4)
  601 FORMAT(1H ,'** CENTRAL ALPHA DENSITY = ',1PE12.4)
      RETURN
      END
C
C     H(X) FOR SLOWING-DOWN DISTRIBUTION
C
      FUNCTION HBEAM(X)
C
      IMPLICIT COMPLEX*16(C),REAL*8(A-B,D-H,O-Z)
C
      SQR3=SQRT(3.D0)
      PI=ACOS(-1.D0)
      HBEAM=(LOG((1.D0+X**3)/(1.D0+X)**3)/3.D0
     &      +(ATAN((2.D0*X-1.D0)/SQR3)+PI/6.D0)/SQR3)/X**2
      RETURN
      END
